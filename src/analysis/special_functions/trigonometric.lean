/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import analysis.special_functions.trigo_without_deriv
import ring_theory.polynomial.chebyshev
import analysis.special_functions.exp_log

/-!
# Trigonometric functions

## Main definitions

This file contains the following definitions:
* œÄ, arcsin, arccos, arctan
* argument of a complex number
* logarithm on complex numbers

## Main statements

Many basic inequalities on trigonometric functions are established.

The continuity and differentiability of the usual trigonometric functions are proved, and their
derivatives are computed.

* `polynomial.chebyshev.T_complex_cos`: the `n`-th Chebyshev polynomial evaluates on `complex.cos Œ∏`
  to the value `complex.cos (n * Œ∏)`.

## Tags

log, sin, cos, tan, arcsin, arccos, arctan, angle, argument
-/

noncomputable theory
open_locale classical topological_space filter
open set filter

namespace complex

/-- The complex sine function is everywhere strictly differentiable, with the derivative `cos x`. -/
lemma has_strict_deriv_at_sin (x : ‚ÑÇ) : has_strict_deriv_at sin (cos x) x :=
begin
  simp only [cos, div_eq_mul_inv],
  convert ((((has_strict_deriv_at_id x).neg.mul_const I).cexp.sub
    ((has_strict_deriv_at_id x).mul_const I).cexp).mul_const I).mul_const (2:‚ÑÇ)‚Åª¬π,
  simp only [function.comp, id],
  rw [sub_mul, mul_assoc, mul_assoc, I_mul_I, neg_one_mul, neg_neg, mul_one, one_mul, mul_assoc,
      I_mul_I, mul_neg_one, sub_neg_eq_add, add_comm]
end

/-- The complex sine function is everywhere differentiable, with the derivative `cos x`. -/
lemma has_deriv_at_sin (x : ‚ÑÇ) : has_deriv_at sin (cos x) x :=
(has_strict_deriv_at_sin x).has_deriv_at

lemma times_cont_diff_sin {n} : times_cont_diff ‚ÑÇ n sin :=
(((times_cont_diff_neg.mul times_cont_diff_const).cexp.sub
  (times_cont_diff_id.mul times_cont_diff_const).cexp).mul times_cont_diff_const).div_const

lemma differentiable_sin : differentiable ‚ÑÇ sin :=
Œªx, (has_deriv_at_sin x).differentiable_at

lemma differentiable_at_sin {x : ‚ÑÇ} : differentiable_at ‚ÑÇ sin x :=
differentiable_sin x

@[simp] lemma deriv_sin : deriv sin = cos :=
funext $ Œª x, (has_deriv_at_sin x).deriv

/-- The complex cosine function is everywhere strictly differentiable, with the derivative
`-sin x`. -/
lemma has_strict_deriv_at_cos (x : ‚ÑÇ) : has_strict_deriv_at cos (-sin x) x :=
begin
  simp only [sin, div_eq_mul_inv, neg_mul_eq_neg_mul],
  convert (((has_strict_deriv_at_id x).mul_const I).cexp.add
    ((has_strict_deriv_at_id x).neg.mul_const I).cexp).mul_const (2:‚ÑÇ)‚Åª¬π,
  simp only [function.comp, id],
  ring
end

/-- The complex cosine function is everywhere differentiable, with the derivative `-sin x`. -/
lemma has_deriv_at_cos (x : ‚ÑÇ) : has_deriv_at cos (-sin x) x :=
(has_strict_deriv_at_cos x).has_deriv_at

lemma times_cont_diff_cos {n} : times_cont_diff ‚ÑÇ n cos :=
((times_cont_diff_id.mul times_cont_diff_const).cexp.add
  (times_cont_diff_neg.mul times_cont_diff_const).cexp).div_const

lemma differentiable_cos : differentiable ‚ÑÇ cos :=
Œªx, (has_deriv_at_cos x).differentiable_at

lemma differentiable_at_cos {x : ‚ÑÇ} : differentiable_at ‚ÑÇ cos x :=
differentiable_cos x

lemma deriv_cos {x : ‚ÑÇ} : deriv cos x = -sin x :=
(has_deriv_at_cos x).deriv

@[simp] lemma deriv_cos' : deriv cos = (Œª x, -sin x) :=
funext $ Œª x, deriv_cos

/-- The complex hyperbolic sine function is everywhere strictly differentiable, with the derivative
`cosh x`. -/
lemma has_strict_deriv_at_sinh (x : ‚ÑÇ) : has_strict_deriv_at sinh (cosh x) x :=
begin
  simp only [cosh, div_eq_mul_inv],
  convert ((has_strict_deriv_at_exp x).sub (has_strict_deriv_at_id x).neg.cexp).mul_const (2:‚ÑÇ)‚Åª¬π,
  rw [id, mul_neg_one, sub_eq_add_neg, neg_neg]
end

/-- The complex hyperbolic sine function is everywhere differentiable, with the derivative
`cosh x`. -/
lemma has_deriv_at_sinh (x : ‚ÑÇ) : has_deriv_at sinh (cosh x) x :=
(has_strict_deriv_at_sinh x).has_deriv_at

lemma times_cont_diff_sinh {n} : times_cont_diff ‚ÑÇ n sinh :=
(times_cont_diff_exp.sub times_cont_diff_neg.cexp).div_const

lemma differentiable_sinh : differentiable ‚ÑÇ sinh :=
Œªx, (has_deriv_at_sinh x).differentiable_at

lemma differentiable_at_sinh {x : ‚ÑÇ} : differentiable_at ‚ÑÇ sinh x :=
differentiable_sinh x

@[simp] lemma deriv_sinh : deriv sinh = cosh :=
funext $ Œª x, (has_deriv_at_sinh x).deriv

/-- The complex hyperbolic cosine function is everywhere strictly differentiable, with the
derivative `sinh x`. -/
lemma has_strict_deriv_at_cosh (x : ‚ÑÇ) : has_strict_deriv_at cosh (sinh x) x :=
begin
  simp only [sinh, div_eq_mul_inv],
  convert ((has_strict_deriv_at_exp x).add (has_strict_deriv_at_id x).neg.cexp).mul_const (2:‚ÑÇ)‚Åª¬π,
  rw [id, mul_neg_one, sub_eq_add_neg]
end

/-- The complex hyperbolic cosine function is everywhere differentiable, with the derivative
`sinh x`. -/
lemma has_deriv_at_cosh (x : ‚ÑÇ) : has_deriv_at cosh (sinh x) x :=
(has_strict_deriv_at_cosh x).has_deriv_at

lemma times_cont_diff_cosh {n} : times_cont_diff ‚ÑÇ n cosh :=
(times_cont_diff_exp.add times_cont_diff_neg.cexp).div_const

lemma differentiable_cosh : differentiable ‚ÑÇ cosh :=
Œªx, (has_deriv_at_cosh x).differentiable_at

lemma differentiable_at_cosh {x : ‚ÑÇ} : differentiable_at ‚ÑÇ cos x :=
differentiable_cos x

@[simp] lemma deriv_cosh : deriv cosh = sinh :=
funext $ Œª x, (has_deriv_at_cosh x).deriv

end complex

section
/-! ### Simp lemmas for derivatives of `Œª x, complex.cos (f x)` etc., `f : ‚ÑÇ ‚Üí ‚ÑÇ` -/

variables {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} {s : set ‚ÑÇ}

/-! #### `complex.cos` -/

lemma has_strict_deriv_at.ccos (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, complex.cos (f x)) (- complex.sin (f x) * f') x :=
(complex.has_strict_deriv_at_cos (f x)).comp x hf

lemma has_deriv_at.ccos (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, complex.cos (f x)) (- complex.sin (f x) * f') x :=
(complex.has_deriv_at_cos (f x)).comp x hf

lemma has_deriv_within_at.ccos (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, complex.cos (f x)) (- complex.sin (f x) * f') s x :=
(complex.has_deriv_at_cos (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_ccos (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  deriv_within (Œªx, complex.cos (f x)) s x = - complex.sin (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.ccos.deriv_within hxs

@[simp] lemma deriv_ccos (hc : differentiable_at ‚ÑÇ f x) :
  deriv (Œªx, complex.cos (f x)) x = - complex.sin (f x) * (deriv f x) :=
hc.has_deriv_at.ccos.deriv

/-! #### `complex.sin` -/

lemma has_strict_deriv_at.csin (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, complex.sin (f x)) (complex.cos (f x) * f') x :=
(complex.has_strict_deriv_at_sin (f x)).comp x hf

lemma has_deriv_at.csin (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, complex.sin (f x)) (complex.cos (f x) * f') x :=
(complex.has_deriv_at_sin (f x)).comp x hf

lemma has_deriv_within_at.csin (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, complex.sin (f x)) (complex.cos (f x) * f') s x :=
(complex.has_deriv_at_sin (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_csin (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  deriv_within (Œªx, complex.sin (f x)) s x = complex.cos (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.csin.deriv_within hxs

@[simp] lemma deriv_csin (hc : differentiable_at ‚ÑÇ f x) :
  deriv (Œªx, complex.sin (f x)) x = complex.cos (f x) * (deriv f x) :=
hc.has_deriv_at.csin.deriv

/-! #### `complex.cosh` -/

lemma has_strict_deriv_at.ccosh (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) * f') x :=
(complex.has_strict_deriv_at_cosh (f x)).comp x hf

lemma has_deriv_at.ccosh (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) * f') x :=
(complex.has_deriv_at_cosh (f x)).comp x hf

lemma has_deriv_within_at.ccosh (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) * f') s x :=
(complex.has_deriv_at_cosh (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_ccosh (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  deriv_within (Œªx, complex.cosh (f x)) s x = complex.sinh (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.ccosh.deriv_within hxs

@[simp] lemma deriv_ccosh (hc : differentiable_at ‚ÑÇ f x) :
  deriv (Œªx, complex.cosh (f x)) x = complex.sinh (f x) * (deriv f x) :=
hc.has_deriv_at.ccosh.deriv

/-! #### `complex.sinh` -/

lemma has_strict_deriv_at.csinh (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) * f') x :=
(complex.has_strict_deriv_at_sinh (f x)).comp x hf

lemma has_deriv_at.csinh (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) * f') x :=
(complex.has_deriv_at_sinh (f x)).comp x hf

lemma has_deriv_within_at.csinh (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) * f') s x :=
(complex.has_deriv_at_sinh (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_csinh (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  deriv_within (Œªx, complex.sinh (f x)) s x = complex.cosh (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.csinh.deriv_within hxs

@[simp] lemma deriv_csinh (hc : differentiable_at ‚ÑÇ f x) :
  deriv (Œªx, complex.sinh (f x)) x = complex.cosh (f x) * (deriv f x) :=
hc.has_deriv_at.csinh.deriv

end

section
/-! ### Simp lemmas for derivatives of `Œª x, complex.cos (f x)` etc., `f : E ‚Üí ‚ÑÇ` -/

variables {E : Type*} [normed_group E] [normed_space ‚ÑÇ E] {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ}
  {x : E} {s : set E}

/-! #### `complex.cos` -/

lemma has_strict_fderiv_at.ccos (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, complex.cos (f x)) (- complex.sin (f x) ‚Ä¢ f') x :=
(complex.has_strict_deriv_at_cos (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.ccos (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, complex.cos (f x)) (- complex.sin (f x) ‚Ä¢ f') x :=
(complex.has_deriv_at_cos (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.ccos (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, complex.cos (f x)) (- complex.sin (f x) ‚Ä¢ f') s x :=
(complex.has_deriv_at_cos (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.ccos (hf : differentiable_within_at ‚ÑÇ f s x) :
  differentiable_within_at ‚ÑÇ (Œª x, complex.cos (f x)) s x :=
hf.has_fderiv_within_at.ccos.differentiable_within_at

@[simp] lemma differentiable_at.ccos (hc : differentiable_at ‚ÑÇ f x) :
  differentiable_at ‚ÑÇ (Œªx, complex.cos (f x)) x :=
hc.has_fderiv_at.ccos.differentiable_at

lemma differentiable_on.ccos (hc : differentiable_on ‚ÑÇ f s) :
  differentiable_on ‚ÑÇ (Œªx, complex.cos (f x)) s :=
Œªx h, (hc x h).ccos

@[simp] lemma differentiable.ccos (hc : differentiable ‚ÑÇ f) :
  differentiable ‚ÑÇ (Œªx, complex.cos (f x)) :=
Œªx, (hc x).ccos

lemma fderiv_within_ccos (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  fderiv_within ‚ÑÇ (Œªx, complex.cos (f x)) s x = - complex.sin (f x) ‚Ä¢ (fderiv_within ‚ÑÇ f s x) :=
hf.has_fderiv_within_at.ccos.fderiv_within hxs

@[simp] lemma fderiv_ccos (hc : differentiable_at ‚ÑÇ f x) :
  fderiv ‚ÑÇ (Œªx, complex.cos (f x)) x = - complex.sin (f x) ‚Ä¢ (fderiv ‚ÑÇ f x) :=
hc.has_fderiv_at.ccos.fderiv

lemma times_cont_diff.ccos {n} (h : times_cont_diff ‚ÑÇ n f) :
  times_cont_diff ‚ÑÇ n (Œª x, complex.cos (f x)) :=
complex.times_cont_diff_cos.comp h

lemma times_cont_diff_at.ccos {n} (hf : times_cont_diff_at ‚ÑÇ n f x) :
  times_cont_diff_at ‚ÑÇ n (Œª x, complex.cos (f x)) x :=
complex.times_cont_diff_cos.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.ccos {n} (hf : times_cont_diff_on ‚ÑÇ n f s) :
  times_cont_diff_on ‚ÑÇ n (Œª x, complex.cos (f x)) s :=
complex.times_cont_diff_cos.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.ccos {n} (hf : times_cont_diff_within_at ‚ÑÇ n f s x) :
  times_cont_diff_within_at ‚ÑÇ n (Œª x, complex.cos (f x)) s x :=
complex.times_cont_diff_cos.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `complex.sin` -/

lemma has_strict_fderiv_at.csin (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, complex.sin (f x)) (complex.cos (f x) ‚Ä¢ f') x :=
(complex.has_strict_deriv_at_sin (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.csin (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, complex.sin (f x)) (complex.cos (f x) ‚Ä¢ f') x :=
(complex.has_deriv_at_sin (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.csin (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, complex.sin (f x)) (complex.cos (f x) ‚Ä¢ f') s x :=
(complex.has_deriv_at_sin (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.csin (hf : differentiable_within_at ‚ÑÇ f s x) :
  differentiable_within_at ‚ÑÇ (Œª x, complex.sin (f x)) s x :=
hf.has_fderiv_within_at.csin.differentiable_within_at

@[simp] lemma differentiable_at.csin (hc : differentiable_at ‚ÑÇ f x) :
  differentiable_at ‚ÑÇ (Œªx, complex.sin (f x)) x :=
hc.has_fderiv_at.csin.differentiable_at

lemma differentiable_on.csin (hc : differentiable_on ‚ÑÇ f s) :
  differentiable_on ‚ÑÇ (Œªx, complex.sin (f x)) s :=
Œªx h, (hc x h).csin

@[simp] lemma differentiable.csin (hc : differentiable ‚ÑÇ f) :
  differentiable ‚ÑÇ (Œªx, complex.sin (f x)) :=
Œªx, (hc x).csin

lemma fderiv_within_csin (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  fderiv_within ‚ÑÇ (Œªx, complex.sin (f x)) s x = complex.cos (f x) ‚Ä¢ (fderiv_within ‚ÑÇ f s x) :=
hf.has_fderiv_within_at.csin.fderiv_within hxs

@[simp] lemma fderiv_csin (hc : differentiable_at ‚ÑÇ f x) :
  fderiv ‚ÑÇ (Œªx, complex.sin (f x)) x = complex.cos (f x) ‚Ä¢ (fderiv ‚ÑÇ f x) :=
hc.has_fderiv_at.csin.fderiv

lemma times_cont_diff.csin {n} (h : times_cont_diff ‚ÑÇ n f) :
  times_cont_diff ‚ÑÇ n (Œª x, complex.sin (f x)) :=
complex.times_cont_diff_sin.comp h

lemma times_cont_diff_at.csin {n} (hf : times_cont_diff_at ‚ÑÇ n f x) :
  times_cont_diff_at ‚ÑÇ n (Œª x, complex.sin (f x)) x :=
complex.times_cont_diff_sin.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.csin {n} (hf : times_cont_diff_on ‚ÑÇ n f s) :
  times_cont_diff_on ‚ÑÇ n (Œª x, complex.sin (f x)) s :=
complex.times_cont_diff_sin.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.csin {n} (hf : times_cont_diff_within_at ‚ÑÇ n f s x) :
  times_cont_diff_within_at ‚ÑÇ n (Œª x, complex.sin (f x)) s x :=
complex.times_cont_diff_sin.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `complex.cosh` -/

lemma has_strict_fderiv_at.ccosh (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) ‚Ä¢ f') x :=
(complex.has_strict_deriv_at_cosh (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.ccosh (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) ‚Ä¢ f') x :=
(complex.has_deriv_at_cosh (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.ccosh (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, complex.cosh (f x)) (complex.sinh (f x) ‚Ä¢ f') s x :=
(complex.has_deriv_at_cosh (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.ccosh (hf : differentiable_within_at ‚ÑÇ f s x) :
  differentiable_within_at ‚ÑÇ (Œª x, complex.cosh (f x)) s x :=
hf.has_fderiv_within_at.ccosh.differentiable_within_at

@[simp] lemma differentiable_at.ccosh (hc : differentiable_at ‚ÑÇ f x) :
  differentiable_at ‚ÑÇ (Œªx, complex.cosh (f x)) x :=
hc.has_fderiv_at.ccosh.differentiable_at

lemma differentiable_on.ccosh (hc : differentiable_on ‚ÑÇ f s) :
  differentiable_on ‚ÑÇ (Œªx, complex.cosh (f x)) s :=
Œªx h, (hc x h).ccosh

@[simp] lemma differentiable.ccosh (hc : differentiable ‚ÑÇ f) :
  differentiable ‚ÑÇ (Œªx, complex.cosh (f x)) :=
Œªx, (hc x).ccosh

lemma fderiv_within_ccosh (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  fderiv_within ‚ÑÇ (Œªx, complex.cosh (f x)) s x = complex.sinh (f x) ‚Ä¢ (fderiv_within ‚ÑÇ f s x) :=
hf.has_fderiv_within_at.ccosh.fderiv_within hxs

@[simp] lemma fderiv_ccosh (hc : differentiable_at ‚ÑÇ f x) :
  fderiv ‚ÑÇ (Œªx, complex.cosh (f x)) x = complex.sinh (f x) ‚Ä¢ (fderiv ‚ÑÇ f x) :=
hc.has_fderiv_at.ccosh.fderiv

lemma times_cont_diff.ccosh {n} (h : times_cont_diff ‚ÑÇ n f) :
  times_cont_diff ‚ÑÇ n (Œª x, complex.cosh (f x)) :=
complex.times_cont_diff_cosh.comp h

lemma times_cont_diff_at.ccosh {n} (hf : times_cont_diff_at ‚ÑÇ n f x) :
  times_cont_diff_at ‚ÑÇ n (Œª x, complex.cosh (f x)) x :=
complex.times_cont_diff_cosh.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.ccosh {n} (hf : times_cont_diff_on ‚ÑÇ n f s) :
  times_cont_diff_on ‚ÑÇ n (Œª x, complex.cosh (f x)) s :=
complex.times_cont_diff_cosh.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.ccosh {n} (hf : times_cont_diff_within_at ‚ÑÇ n f s x) :
  times_cont_diff_within_at ‚ÑÇ n (Œª x, complex.cosh (f x)) s x :=
complex.times_cont_diff_cosh.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `complex.sinh` -/

lemma has_strict_fderiv_at.csinh (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) ‚Ä¢ f') x :=
(complex.has_strict_deriv_at_sinh (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.csinh (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) ‚Ä¢ f') x :=
(complex.has_deriv_at_sinh (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.csinh (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, complex.sinh (f x)) (complex.cosh (f x) ‚Ä¢ f') s x :=
(complex.has_deriv_at_sinh (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.csinh (hf : differentiable_within_at ‚ÑÇ f s x) :
  differentiable_within_at ‚ÑÇ (Œª x, complex.sinh (f x)) s x :=
hf.has_fderiv_within_at.csinh.differentiable_within_at

@[simp] lemma differentiable_at.csinh (hc : differentiable_at ‚ÑÇ f x) :
  differentiable_at ‚ÑÇ (Œªx, complex.sinh (f x)) x :=
hc.has_fderiv_at.csinh.differentiable_at

lemma differentiable_on.csinh (hc : differentiable_on ‚ÑÇ f s) :
  differentiable_on ‚ÑÇ (Œªx, complex.sinh (f x)) s :=
Œªx h, (hc x h).csinh

@[simp] lemma differentiable.csinh (hc : differentiable ‚ÑÇ f) :
  differentiable ‚ÑÇ (Œªx, complex.sinh (f x)) :=
Œªx, (hc x).csinh

lemma fderiv_within_csinh (hf : differentiable_within_at ‚ÑÇ f s x)
  (hxs : unique_diff_within_at ‚ÑÇ s x) :
  fderiv_within ‚ÑÇ (Œªx, complex.sinh (f x)) s x = complex.cosh (f x) ‚Ä¢ (fderiv_within ‚ÑÇ f s x) :=
hf.has_fderiv_within_at.csinh.fderiv_within hxs

@[simp] lemma fderiv_csinh (hc : differentiable_at ‚ÑÇ f x) :
  fderiv ‚ÑÇ (Œªx, complex.sinh (f x)) x = complex.cosh (f x) ‚Ä¢ (fderiv ‚ÑÇ f x) :=
hc.has_fderiv_at.csinh.fderiv

lemma times_cont_diff.csinh {n} (h : times_cont_diff ‚ÑÇ n f) :
  times_cont_diff ‚ÑÇ n (Œª x, complex.sinh (f x)) :=
complex.times_cont_diff_sinh.comp h

lemma times_cont_diff_at.csinh {n} (hf : times_cont_diff_at ‚ÑÇ n f x) :
  times_cont_diff_at ‚ÑÇ n (Œª x, complex.sinh (f x)) x :=
complex.times_cont_diff_sinh.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.csinh {n} (hf : times_cont_diff_on ‚ÑÇ n f s) :
  times_cont_diff_on ‚ÑÇ n (Œª x, complex.sinh (f x)) s :=
complex.times_cont_diff_sinh.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.csinh {n} (hf : times_cont_diff_within_at ‚ÑÇ n f s x) :
  times_cont_diff_within_at ‚ÑÇ n (Œª x, complex.sinh (f x)) s x :=
complex.times_cont_diff_sinh.times_cont_diff_at.comp_times_cont_diff_within_at x hf

end

namespace real

variables {x y z : ‚Ñù}

lemma has_strict_deriv_at_sin (x : ‚Ñù) : has_strict_deriv_at sin (cos x) x :=
(complex.has_strict_deriv_at_sin x).real_of_complex

lemma has_deriv_at_sin (x : ‚Ñù) : has_deriv_at sin (cos x) x :=
(has_strict_deriv_at_sin x).has_deriv_at

lemma times_cont_diff_sin {n} : times_cont_diff ‚Ñù n sin :=
complex.times_cont_diff_sin.real_of_complex

lemma differentiable_sin : differentiable ‚Ñù sin :=
Œªx, (has_deriv_at_sin x).differentiable_at

lemma differentiable_at_sin : differentiable_at ‚Ñù sin x :=
differentiable_sin x

@[simp] lemma deriv_sin : deriv sin = cos :=
funext $ Œª x, (has_deriv_at_sin x).deriv

lemma has_strict_deriv_at_cos (x : ‚Ñù) : has_strict_deriv_at cos (-sin x) x :=
(complex.has_strict_deriv_at_cos x).real_of_complex

lemma has_deriv_at_cos (x : ‚Ñù) : has_deriv_at cos (-sin x) x :=
(complex.has_deriv_at_cos x).real_of_complex

lemma times_cont_diff_cos {n} : times_cont_diff ‚Ñù n cos :=
complex.times_cont_diff_cos.real_of_complex

lemma differentiable_cos : differentiable ‚Ñù cos :=
Œªx, (has_deriv_at_cos x).differentiable_at

lemma differentiable_at_cos : differentiable_at ‚Ñù cos x :=
differentiable_cos x

lemma deriv_cos : deriv cos x = - sin x :=
(has_deriv_at_cos x).deriv

@[simp] lemma deriv_cos' : deriv cos = (Œª x, - sin x) :=
funext $ Œª _, deriv_cos

lemma has_strict_deriv_at_sinh (x : ‚Ñù) : has_strict_deriv_at sinh (cosh x) x :=
(complex.has_strict_deriv_at_sinh x).real_of_complex

lemma has_deriv_at_sinh (x : ‚Ñù) : has_deriv_at sinh (cosh x) x :=
(complex.has_deriv_at_sinh x).real_of_complex

lemma times_cont_diff_sinh {n} : times_cont_diff ‚Ñù n sinh :=
complex.times_cont_diff_sinh.real_of_complex

lemma differentiable_sinh : differentiable ‚Ñù sinh :=
Œªx, (has_deriv_at_sinh x).differentiable_at

lemma differentiable_at_sinh : differentiable_at ‚Ñù sinh x :=
differentiable_sinh x

@[simp] lemma deriv_sinh : deriv sinh = cosh :=
funext $ Œª x, (has_deriv_at_sinh x).deriv

lemma has_strict_deriv_at_cosh (x : ‚Ñù) : has_strict_deriv_at cosh (sinh x) x :=
(complex.has_strict_deriv_at_cosh x).real_of_complex

lemma has_deriv_at_cosh (x : ‚Ñù) : has_deriv_at cosh (sinh x) x :=
(complex.has_deriv_at_cosh x).real_of_complex

lemma times_cont_diff_cosh {n} : times_cont_diff ‚Ñù n cosh :=
complex.times_cont_diff_cosh.real_of_complex

lemma differentiable_cosh : differentiable ‚Ñù cosh :=
Œªx, (has_deriv_at_cosh x).differentiable_at

lemma differentiable_at_cosh : differentiable_at ‚Ñù cosh x :=
differentiable_cosh x

@[simp] lemma deriv_cosh : deriv cosh = sinh :=
funext $ Œª x, (has_deriv_at_cosh x).deriv

/-- `sinh` is strictly monotone. -/
lemma sinh_strict_mono : strict_mono sinh :=
strict_mono_of_deriv_pos differentiable_sinh (by { rw [real.deriv_sinh], exact cosh_pos })

end real

section
/-! ### Simp lemmas for derivatives of `Œª x, real.cos (f x)` etc., `f : ‚Ñù ‚Üí ‚Ñù` -/

variables {f : ‚Ñù ‚Üí ‚Ñù} {f' x : ‚Ñù} {s : set ‚Ñù}

/-! #### `real.cos` -/

lemma has_strict_deriv_at.cos (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, real.cos (f x)) (- real.sin (f x) * f') x :=
(real.has_strict_deriv_at_cos (f x)).comp x hf

lemma has_deriv_at.cos (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, real.cos (f x)) (- real.sin (f x) * f') x :=
(real.has_deriv_at_cos (f x)).comp x hf

lemma has_deriv_within_at.cos (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, real.cos (f x)) (- real.sin (f x) * f') s x :=
(real.has_deriv_at_cos (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_cos (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, real.cos (f x)) s x = - real.sin (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.cos.deriv_within hxs

@[simp] lemma deriv_cos (hc : differentiable_at ‚Ñù f x) :
  deriv (Œªx, real.cos (f x)) x = - real.sin (f x) * (deriv f x) :=
hc.has_deriv_at.cos.deriv

/-! #### `real.sin` -/

lemma has_strict_deriv_at.sin (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, real.sin (f x)) (real.cos (f x) * f') x :=
(real.has_strict_deriv_at_sin (f x)).comp x hf

lemma has_deriv_at.sin (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, real.sin (f x)) (real.cos (f x) * f') x :=
(real.has_deriv_at_sin (f x)).comp x hf

lemma has_deriv_within_at.sin (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, real.sin (f x)) (real.cos (f x) * f') s x :=
(real.has_deriv_at_sin (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_sin (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, real.sin (f x)) s x = real.cos (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.sin.deriv_within hxs

@[simp] lemma deriv_sin (hc : differentiable_at ‚Ñù f x) :
  deriv (Œªx, real.sin (f x)) x = real.cos (f x) * (deriv f x) :=
hc.has_deriv_at.sin.deriv

/-! #### `real.cosh` -/

lemma has_strict_deriv_at.cosh (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, real.cosh (f x)) (real.sinh (f x) * f') x :=
(real.has_strict_deriv_at_cosh (f x)).comp x hf

lemma has_deriv_at.cosh (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, real.cosh (f x)) (real.sinh (f x) * f') x :=
(real.has_deriv_at_cosh (f x)).comp x hf

lemma has_deriv_within_at.cosh (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, real.cosh (f x)) (real.sinh (f x) * f') s x :=
(real.has_deriv_at_cosh (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_cosh (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, real.cosh (f x)) s x = real.sinh (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.cosh.deriv_within hxs

@[simp] lemma deriv_cosh (hc : differentiable_at ‚Ñù f x) :
  deriv (Œªx, real.cosh (f x)) x = real.sinh (f x) * (deriv f x) :=
hc.has_deriv_at.cosh.deriv

/-! #### `real.sinh` -/

lemma has_strict_deriv_at.sinh (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, real.sinh (f x)) (real.cosh (f x) * f') x :=
(real.has_strict_deriv_at_sinh (f x)).comp x hf

lemma has_deriv_at.sinh (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, real.sinh (f x)) (real.cosh (f x) * f') x :=
(real.has_deriv_at_sinh (f x)).comp x hf

lemma has_deriv_within_at.sinh (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, real.sinh (f x)) (real.cosh (f x) * f') s x :=
(real.has_deriv_at_sinh (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_sinh (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, real.sinh (f x)) s x = real.cosh (f x) * (deriv_within f s x) :=
hf.has_deriv_within_at.sinh.deriv_within hxs

@[simp] lemma deriv_sinh (hc : differentiable_at ‚Ñù f x) :
  deriv (Œªx, real.sinh (f x)) x = real.cosh (f x) * (deriv f x) :=
hc.has_deriv_at.sinh.deriv

end

section

/-! ### Simp lemmas for derivatives of `Œª x, real.cos (f x)` etc., `f : E ‚Üí ‚Ñù` -/

variables {E : Type*} [normed_group E] [normed_space ‚Ñù E] {f : E ‚Üí ‚Ñù} {f' : E ‚ÜíL[‚Ñù] ‚Ñù}
  {x : E} {s : set E}

/-! #### `real.cos` -/

lemma has_strict_fderiv_at.cos (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, real.cos (f x)) (- real.sin (f x) ‚Ä¢ f') x :=
(real.has_strict_deriv_at_cos (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.cos (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, real.cos (f x)) (- real.sin (f x) ‚Ä¢ f') x :=
(real.has_deriv_at_cos (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.cos (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, real.cos (f x)) (- real.sin (f x) ‚Ä¢ f') s x :=
(real.has_deriv_at_cos (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.cos (hf : differentiable_within_at ‚Ñù f s x) :
  differentiable_within_at ‚Ñù (Œª x, real.cos (f x)) s x :=
hf.has_fderiv_within_at.cos.differentiable_within_at

@[simp] lemma differentiable_at.cos (hc : differentiable_at ‚Ñù f x) :
  differentiable_at ‚Ñù (Œªx, real.cos (f x)) x :=
hc.has_fderiv_at.cos.differentiable_at

lemma differentiable_on.cos (hc : differentiable_on ‚Ñù f s) :
  differentiable_on ‚Ñù (Œªx, real.cos (f x)) s :=
Œªx h, (hc x h).cos

@[simp] lemma differentiable.cos (hc : differentiable ‚Ñù f) :
  differentiable ‚Ñù (Œªx, real.cos (f x)) :=
Œªx, (hc x).cos

lemma fderiv_within_cos (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  fderiv_within ‚Ñù (Œªx, real.cos (f x)) s x = - real.sin (f x) ‚Ä¢ (fderiv_within ‚Ñù f s x) :=
hf.has_fderiv_within_at.cos.fderiv_within hxs

@[simp] lemma fderiv_cos (hc : differentiable_at ‚Ñù f x) :
  fderiv ‚Ñù (Œªx, real.cos (f x)) x = - real.sin (f x) ‚Ä¢ (fderiv ‚Ñù f x) :=
hc.has_fderiv_at.cos.fderiv

lemma times_cont_diff.cos {n} (h : times_cont_diff ‚Ñù n f) :
  times_cont_diff ‚Ñù n (Œª x, real.cos (f x)) :=
real.times_cont_diff_cos.comp h

lemma times_cont_diff_at.cos {n} (hf : times_cont_diff_at ‚Ñù n f x) :
  times_cont_diff_at ‚Ñù n (Œª x, real.cos (f x)) x :=
real.times_cont_diff_cos.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.cos {n} (hf : times_cont_diff_on ‚Ñù n f s) :
  times_cont_diff_on ‚Ñù n (Œª x, real.cos (f x)) s :=
real.times_cont_diff_cos.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.cos {n} (hf : times_cont_diff_within_at ‚Ñù n f s x) :
  times_cont_diff_within_at ‚Ñù n (Œª x, real.cos (f x)) s x :=
real.times_cont_diff_cos.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `real.sin` -/

lemma has_strict_fderiv_at.sin (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, real.sin (f x)) (real.cos (f x) ‚Ä¢ f') x :=
(real.has_strict_deriv_at_sin (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.sin (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, real.sin (f x)) (real.cos (f x) ‚Ä¢ f') x :=
(real.has_deriv_at_sin (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.sin (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, real.sin (f x)) (real.cos (f x) ‚Ä¢ f') s x :=
(real.has_deriv_at_sin (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.sin (hf : differentiable_within_at ‚Ñù f s x) :
  differentiable_within_at ‚Ñù (Œª x, real.sin (f x)) s x :=
hf.has_fderiv_within_at.sin.differentiable_within_at

@[simp] lemma differentiable_at.sin (hc : differentiable_at ‚Ñù f x) :
  differentiable_at ‚Ñù (Œªx, real.sin (f x)) x :=
hc.has_fderiv_at.sin.differentiable_at

lemma differentiable_on.sin (hc : differentiable_on ‚Ñù f s) :
  differentiable_on ‚Ñù (Œªx, real.sin (f x)) s :=
Œªx h, (hc x h).sin

@[simp] lemma differentiable.sin (hc : differentiable ‚Ñù f) :
  differentiable ‚Ñù (Œªx, real.sin (f x)) :=
Œªx, (hc x).sin

lemma fderiv_within_sin (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  fderiv_within ‚Ñù (Œªx, real.sin (f x)) s x = real.cos (f x) ‚Ä¢ (fderiv_within ‚Ñù f s x) :=
hf.has_fderiv_within_at.sin.fderiv_within hxs

@[simp] lemma fderiv_sin (hc : differentiable_at ‚Ñù f x) :
  fderiv ‚Ñù (Œªx, real.sin (f x)) x = real.cos (f x) ‚Ä¢ (fderiv ‚Ñù f x) :=
hc.has_fderiv_at.sin.fderiv

lemma times_cont_diff.sin {n} (h : times_cont_diff ‚Ñù n f) :
  times_cont_diff ‚Ñù n (Œª x, real.sin (f x)) :=
real.times_cont_diff_sin.comp h

lemma times_cont_diff_at.sin {n} (hf : times_cont_diff_at ‚Ñù n f x) :
  times_cont_diff_at ‚Ñù n (Œª x, real.sin (f x)) x :=
real.times_cont_diff_sin.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.sin {n} (hf : times_cont_diff_on ‚Ñù n f s) :
  times_cont_diff_on ‚Ñù n (Œª x, real.sin (f x)) s :=
real.times_cont_diff_sin.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.sin {n} (hf : times_cont_diff_within_at ‚Ñù n f s x) :
  times_cont_diff_within_at ‚Ñù n (Œª x, real.sin (f x)) s x :=
real.times_cont_diff_sin.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `real.cosh` -/

lemma has_strict_fderiv_at.cosh (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, real.cosh (f x)) (real.sinh (f x) ‚Ä¢ f') x :=
(real.has_strict_deriv_at_cosh (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.cosh (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, real.cosh (f x)) (real.sinh (f x) ‚Ä¢ f') x :=
(real.has_deriv_at_cosh (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.cosh (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, real.cosh (f x)) (real.sinh (f x) ‚Ä¢ f') s x :=
(real.has_deriv_at_cosh (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.cosh (hf : differentiable_within_at ‚Ñù f s x) :
  differentiable_within_at ‚Ñù (Œª x, real.cosh (f x)) s x :=
hf.has_fderiv_within_at.cosh.differentiable_within_at

@[simp] lemma differentiable_at.cosh (hc : differentiable_at ‚Ñù f x) :
  differentiable_at ‚Ñù (Œªx, real.cosh (f x)) x :=
hc.has_fderiv_at.cosh.differentiable_at

lemma differentiable_on.cosh (hc : differentiable_on ‚Ñù f s) :
  differentiable_on ‚Ñù (Œªx, real.cosh (f x)) s :=
Œªx h, (hc x h).cosh

@[simp] lemma differentiable.cosh (hc : differentiable ‚Ñù f) :
  differentiable ‚Ñù (Œªx, real.cosh (f x)) :=
Œªx, (hc x).cosh

lemma fderiv_within_cosh (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  fderiv_within ‚Ñù (Œªx, real.cosh (f x)) s x = real.sinh (f x) ‚Ä¢ (fderiv_within ‚Ñù f s x) :=
hf.has_fderiv_within_at.cosh.fderiv_within hxs

@[simp] lemma fderiv_cosh (hc : differentiable_at ‚Ñù f x) :
  fderiv ‚Ñù (Œªx, real.cosh (f x)) x = real.sinh (f x) ‚Ä¢ (fderiv ‚Ñù f x) :=
hc.has_fderiv_at.cosh.fderiv

lemma times_cont_diff.cosh {n} (h : times_cont_diff ‚Ñù n f) :
  times_cont_diff ‚Ñù n (Œª x, real.cosh (f x)) :=
real.times_cont_diff_cosh.comp h

lemma times_cont_diff_at.cosh {n} (hf : times_cont_diff_at ‚Ñù n f x) :
  times_cont_diff_at ‚Ñù n (Œª x, real.cosh (f x)) x :=
real.times_cont_diff_cosh.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.cosh {n} (hf : times_cont_diff_on ‚Ñù n f s) :
  times_cont_diff_on ‚Ñù n (Œª x, real.cosh (f x)) s :=
real.times_cont_diff_cosh.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.cosh {n} (hf : times_cont_diff_within_at ‚Ñù n f s x) :
  times_cont_diff_within_at ‚Ñù n (Œª x, real.cosh (f x)) s x :=
real.times_cont_diff_cosh.times_cont_diff_at.comp_times_cont_diff_within_at x hf

/-! #### `real.sinh` -/

lemma has_strict_fderiv_at.sinh (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, real.sinh (f x)) (real.cosh (f x) ‚Ä¢ f') x :=
(real.has_strict_deriv_at_sinh (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.sinh (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, real.sinh (f x)) (real.cosh (f x) ‚Ä¢ f') x :=
(real.has_deriv_at_sinh (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.sinh (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, real.sinh (f x)) (real.cosh (f x) ‚Ä¢ f') s x :=
(real.has_deriv_at_sinh (f x)).comp_has_fderiv_within_at x hf

lemma differentiable_within_at.sinh (hf : differentiable_within_at ‚Ñù f s x) :
  differentiable_within_at ‚Ñù (Œª x, real.sinh (f x)) s x :=
hf.has_fderiv_within_at.sinh.differentiable_within_at

@[simp] lemma differentiable_at.sinh (hc : differentiable_at ‚Ñù f x) :
  differentiable_at ‚Ñù (Œªx, real.sinh (f x)) x :=
hc.has_fderiv_at.sinh.differentiable_at

lemma differentiable_on.sinh (hc : differentiable_on ‚Ñù f s) :
  differentiable_on ‚Ñù (Œªx, real.sinh (f x)) s :=
Œªx h, (hc x h).sinh

@[simp] lemma differentiable.sinh (hc : differentiable ‚Ñù f) :
  differentiable ‚Ñù (Œªx, real.sinh (f x)) :=
Œªx, (hc x).sinh

lemma fderiv_within_sinh (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  fderiv_within ‚Ñù (Œªx, real.sinh (f x)) s x = real.cosh (f x) ‚Ä¢ (fderiv_within ‚Ñù f s x) :=
hf.has_fderiv_within_at.sinh.fderiv_within hxs

@[simp] lemma fderiv_sinh (hc : differentiable_at ‚Ñù f x) :
  fderiv ‚Ñù (Œªx, real.sinh (f x)) x = real.cosh (f x) ‚Ä¢ (fderiv ‚Ñù f x) :=
hc.has_fderiv_at.sinh.fderiv

lemma times_cont_diff.sinh {n} (h : times_cont_diff ‚Ñù n f) :
  times_cont_diff ‚Ñù n (Œª x, real.sinh (f x)) :=
real.times_cont_diff_sinh.comp h

lemma times_cont_diff_at.sinh {n} (hf : times_cont_diff_at ‚Ñù n f x) :
  times_cont_diff_at ‚Ñù n (Œª x, real.sinh (f x)) x :=
real.times_cont_diff_sinh.times_cont_diff_at.comp x hf

lemma times_cont_diff_on.sinh {n} (hf : times_cont_diff_on ‚Ñù n f s) :
  times_cont_diff_on ‚Ñù n (Œª x, real.sinh (f x)) s :=
real.times_cont_diff_sinh.comp_times_cont_diff_on  hf

lemma times_cont_diff_within_at.sinh {n} (hf : times_cont_diff_within_at ‚Ñù n f s x) :
  times_cont_diff_within_at ‚Ñù n (Œª x, real.sinh (f x)) s x :=
real.times_cont_diff_sinh.times_cont_diff_at.comp_times_cont_diff_within_at x hf

end

namespace real
open_locale real

lemma deriv_arcsin_aux {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x ‚àß times_cont_diff_at ‚Ñù ‚ä§ arcsin x :=
begin
  cases h‚ÇÅ.lt_or_lt with h‚ÇÅ h‚ÇÅ,
  { have : 1 - x ^ 2 < 0, by nlinarith [h‚ÇÅ],
    rw [sqrt_eq_zero'.2 this.le, div_zero],
    have : arcsin =·∂†[ùìù x] Œª _, -(œÄ / 2) :=
      (gt_mem_nhds h‚ÇÅ).mono (Œª y hy, arcsin_of_le_neg_one hy.le),
    exact ‚ü®(has_strict_deriv_at_const _ _).congr_of_eventually_eq this.symm,
      times_cont_diff_at_const.congr_of_eventually_eq this‚ü© },
  cases h‚ÇÇ.lt_or_lt with h‚ÇÇ h‚ÇÇ,
  { have : 0 < sqrt (1 - x ^ 2) := sqrt_pos.2 (by nlinarith [h‚ÇÅ, h‚ÇÇ]),
    simp only [‚Üê cos_arcsin h‚ÇÅ.le h‚ÇÇ.le, one_div] at this ‚ä¢,
    exact ‚ü®sin_local_homeomorph.has_strict_deriv_at_symm ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© this.ne'
      (has_strict_deriv_at_sin _),
      sin_local_homeomorph.times_cont_diff_at_symm_deriv this.ne' ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
        (has_deriv_at_sin _) times_cont_diff_sin.times_cont_diff_at‚ü© },
  { have : 1 - x ^ 2 < 0, by nlinarith [h‚ÇÇ],
    rw [sqrt_eq_zero'.2 this.le, div_zero],
    have : arcsin =·∂†[ùìù x] Œª _, œÄ / 2 := (lt_mem_nhds h‚ÇÇ).mono (Œª y hy, arcsin_of_one_le hy.le),
    exact ‚ü®(has_strict_deriv_at_const _ _).congr_of_eventually_eq this.symm,
      times_cont_diff_at_const.congr_of_eventually_eq this‚ü© }
end

lemma has_strict_deriv_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x :=
(deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).1

lemma has_deriv_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x :=
(has_strict_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).has_deriv_at

lemma times_cont_diff_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arcsin x :=
(deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).2.of_le le_top

lemma has_deriv_within_at_arcsin_Ici {x : ‚Ñù} (h : x ‚â† -1) :
  has_deriv_within_at arcsin (1 / sqrt (1 - x ^ 2)) (Ici x) x :=
begin
  rcases em (x = 1) with (rfl|h'),
  { convert (has_deriv_within_at_const _ _ (œÄ / 2)).congr _ _;
      simp [arcsin_of_one_le] { contextual := tt } },
  { exact (has_deriv_at_arcsin h h').has_deriv_within_at }
end

lemma has_deriv_within_at_arcsin_Iic {x : ‚Ñù} (h : x ‚â† 1) :
  has_deriv_within_at arcsin (1 / sqrt (1 - x ^ 2)) (Iic x) x :=
begin
  rcases em (x = -1) with (rfl|h'),
  { convert (has_deriv_within_at_const _ _ (-(œÄ / 2))).congr _ _;
      simp [arcsin_of_le_neg_one] { contextual := tt } },
  { exact (has_deriv_at_arcsin h' h).has_deriv_within_at }
end

lemma differentiable_within_at_arcsin_Ici {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arcsin (Ici x) x ‚Üî x ‚â† -1 :=
begin
  refine ‚ü®_, Œª h, (has_deriv_within_at_arcsin_Ici h).differentiable_within_at‚ü©,
  rintro h rfl,
  have : sin ‚àò arcsin =·∂†[ùìù[Ici (-1:‚Ñù)] (-1)] id,
  { filter_upwards [Icc_mem_nhds_within_Ici ‚ü®le_rfl, neg_lt_self (@zero_lt_one ‚Ñù _ _)‚ü©],
    exact Œª x, sin_arcsin' },
  have := h.has_deriv_within_at.sin.congr_of_eventually_eq this.symm (by simp),
  simpa using (unique_diff_on_Ici _ _ left_mem_Ici).eq_deriv _ this (has_deriv_within_at_id _ _)
end

lemma differentiable_within_at_arcsin_Iic {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arcsin (Iic x) x ‚Üî x ‚â† 1 :=
begin
  refine ‚ü®Œª h, _, Œª h, (has_deriv_within_at_arcsin_Iic h).differentiable_within_at‚ü©,
  rw [‚Üê neg_neg x, ‚Üê image_neg_Ici] at h,
  have := (h.comp (-x) differentiable_within_at_id.neg (maps_to_image _ _)).neg,
  simpa [(‚àò), differentiable_within_at_arcsin_Ici] using this
end

lemma differentiable_at_arcsin {x : ‚Ñù} :
  differentiable_at ‚Ñù arcsin x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
‚ü®Œª h, ‚ü®differentiable_within_at_arcsin_Ici.1 h.differentiable_within_at,
  differentiable_within_at_arcsin_Iic.1 h.differentiable_within_at‚ü©,
  Œª h, (has_deriv_at_arcsin h.1 h.2).differentiable_at‚ü©

@[simp] lemma deriv_arcsin : deriv arcsin = Œª x, 1 / sqrt (1 - x ^ 2) :=
begin
  funext x,
  by_cases h : x ‚â† -1 ‚àß x ‚â† 1,
  { exact (has_deriv_at_arcsin h.1 h.2).deriv },
  { rw [deriv_zero_of_not_differentiable_at (mt differentiable_at_arcsin.1 h)],
    simp only [not_and_distrib, ne.def, not_not] at h,
    rcases h with (rfl|rfl); simp }
end

lemma differentiable_on_arcsin : differentiable_on ‚Ñù arcsin {-1, 1}·∂ú :=
Œª x hx, (differentiable_at_arcsin.2
  ‚ü®Œª h, hx (or.inl h), Œª h, hx (or.inr h)‚ü©).differentiable_within_at

lemma times_cont_diff_on_arcsin {n : with_top ‚Ñï} :
  times_cont_diff_on ‚Ñù n arcsin {-1, 1}·∂ú :=
Œª x hx, (times_cont_diff_at_arcsin (mt or.inl hx) (mt or.inr hx)).times_cont_diff_within_at

lemma times_cont_diff_at_arcsin_iff {x : ‚Ñù} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arcsin x ‚Üî n = 0 ‚à® (x ‚â† -1 ‚àß x ‚â† 1) :=
‚ü®Œª h, or_iff_not_imp_left.2 $ Œª hn, differentiable_at_arcsin.1 $ h.differentiable_at $
  with_top.one_le_iff_pos.2 (pos_iff_ne_zero.2 hn),
  Œª h, h.elim (Œª hn, hn.symm ‚ñ∏ (times_cont_diff_zero.2 continuous_arcsin).times_cont_diff_at) $
    Œª hx, times_cont_diff_at_arcsin hx.1 hx.2‚ü©

lemma has_strict_deriv_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arccos (-(1 / sqrt (1 - x ^ 2))) x :=
(has_strict_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

lemma has_deriv_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_deriv_at arccos (-(1 / sqrt (1 - x ^ 2))) x :=
(has_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

lemma times_cont_diff_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arccos x :=
times_cont_diff_at_const.sub (times_cont_diff_at_arcsin h‚ÇÅ h‚ÇÇ)

lemma has_deriv_within_at_arccos_Ici {x : ‚Ñù} (h : x ‚â† -1) :
  has_deriv_within_at arccos (-(1 / sqrt (1 - x ^ 2))) (Ici x) x :=
(has_deriv_within_at_arcsin_Ici h).const_sub _

lemma has_deriv_within_at_arccos_Iic {x : ‚Ñù} (h : x ‚â† 1) :
  has_deriv_within_at arccos (-(1 / sqrt (1 - x ^ 2))) (Iic x) x :=
(has_deriv_within_at_arcsin_Iic h).const_sub _

lemma differentiable_within_at_arccos_Ici {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arccos (Ici x) x ‚Üî x ‚â† -1 :=
(differentiable_within_at_const_sub_iff _).trans differentiable_within_at_arcsin_Ici

lemma differentiable_within_at_arccos_Iic {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arccos (Iic x) x ‚Üî x ‚â† 1 :=
(differentiable_within_at_const_sub_iff _).trans differentiable_within_at_arcsin_Iic

lemma differentiable_at_arccos {x : ‚Ñù} :
  differentiable_at ‚Ñù arccos x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
(differentiable_at_const_sub_iff _).trans differentiable_at_arcsin

@[simp] lemma deriv_arccos : deriv arccos = Œª x, -(1 / sqrt (1 - x ^ 2)) :=
funext $ Œª x, (deriv_const_sub _).trans $ by simp only [deriv_arcsin]

lemma differentiable_on_arccos : differentiable_on ‚Ñù arccos {-1, 1}·∂ú :=
differentiable_on_arcsin.const_sub _

lemma times_cont_diff_on_arccos {n : with_top ‚Ñï} :
  times_cont_diff_on ‚Ñù n arccos {-1, 1}·∂ú :=
times_cont_diff_on_const.sub times_cont_diff_on_arcsin

lemma times_cont_diff_at_arccos_iff {x : ‚Ñù} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arccos x ‚Üî n = 0 ‚à® (x ‚â† -1 ‚àß x ‚â† 1) :=
by refine iff.trans ‚ü®Œª h, _, Œª h, _‚ü© times_cont_diff_at_arcsin_iff;
  simpa [arccos] using (@times_cont_diff_at_const _ _ _ _ _ _ _ _ _ _ (œÄ / 2)).sub h

end real

namespace complex

open_locale real

/-- `complex.exp` as a `local_homeomorph` with `source = {z | -œÄ < im z < œÄ}` and
`target = {z | 0 < re z} ‚à™ {z | im z ‚â† 0}`. This definition is used to prove that `complex.log`
is complex differentiable at all points but the negative real semi-axis. -/
def exp_local_homeomorph : local_homeomorph ‚ÑÇ ‚ÑÇ :=
local_homeomorph.of_continuous_open
{ to_fun := exp,
  inv_fun := log,
  source := {z : ‚ÑÇ | z.im ‚àà Ioo (- œÄ) œÄ},
  target := {z : ‚ÑÇ | 0 < z.re} ‚à™ {z : ‚ÑÇ | z.im ‚â† 0},
  map_source' :=
    begin
      rintro ‚ü®x, y‚ü© ‚ü®h‚ÇÅ : -œÄ < y, h‚ÇÇ : y < œÄ‚ü©,
      refine (not_or_of_imp $ Œª hz, _).symm,
      obtain rfl : y = 0,
      { rw exp_im at hz,
        simpa [(real.exp_pos _).ne', real.sin_eq_zero_iff_of_lt_of_lt h‚ÇÅ h‚ÇÇ] using hz },
      rw [mem_set_of_eq, ‚Üê of_real_def, exp_of_real_re],
      exact real.exp_pos x
    end,
  map_target' := Œª z h,
    suffices 0 ‚â§ z.re ‚à® z.im ‚â† 0,
      by simpa [log_im, neg_pi_lt_arg, (arg_le_pi _).lt_iff_ne, arg_eq_pi_iff, not_and_distrib],
    h.imp (Œª h, le_of_lt h) id,
  left_inv' := Œª x hx, log_exp hx.1 (le_of_lt hx.2),
  right_inv' := Œª x hx, exp_log $ by { rintro rfl, simpa [lt_irrefl] using hx } }
continuous_exp.continuous_on is_open_map_exp (is_open_Ioo.preimage continuous_im)

lemma has_strict_deriv_at_log {x : ‚ÑÇ} (h : 0 < x.re ‚à® x.im ‚â† 0) :
  has_strict_deriv_at log x‚Åª¬π x :=
have h0 :  x ‚â† 0, by { rintro rfl, simpa [lt_irrefl] using h },
exp_local_homeomorph.has_strict_deriv_at_symm h h0 $
  by simpa [exp_log h0] using has_strict_deriv_at_exp (log x)

lemma times_cont_diff_at_log {x : ‚ÑÇ} (h : 0 < x.re ‚à® x.im ‚â† 0) {n : with_top ‚Ñï} :
  times_cont_diff_at ‚ÑÇ n log x :=
exp_local_homeomorph.times_cont_diff_at_symm_deriv (exp_ne_zero $ log x) h
  (has_deriv_at_exp _) times_cont_diff_exp.times_cont_diff_at

lemma has_strict_deriv_at_tan {x : ‚ÑÇ} (h : cos x ‚â† 0) :
  has_strict_deriv_at tan (1 / (cos x)^2) x :=
begin
  convert (has_strict_deriv_at_sin x).div (has_strict_deriv_at_cos x) h,
  rw ‚Üê sin_sq_add_cos_sq x,
  ring,
end

lemma has_deriv_at_tan {x : ‚ÑÇ} (h : cos x ‚â† 0) :
  has_deriv_at tan (1 / (cos x)^2) x :=
(has_strict_deriv_at_tan h).has_deriv_at

lemma tendsto_abs_tan_of_cos_eq_zero {x : ‚ÑÇ} (hx : cos x = 0) :
  tendsto (Œª x, abs (tan x)) (ùìù[{x}·∂ú] x) at_top :=
begin
  simp only [tan_eq_sin_div_cos, ‚Üê norm_eq_abs, normed_field.norm_div],
  have A : sin x ‚â† 0 := Œª h, by simpa [*, sq] using sin_sq_add_cos_sq x,
  have B : tendsto cos (ùìù[{x}·∂ú] (x)) (ùìù[{0}·∂ú] 0),
  { refine tendsto_inf.2 ‚ü®tendsto.mono_left _ inf_le_left, tendsto_principal.2 _‚ü©,
    exacts [continuous_cos.tendsto' x 0 hx,
      hx ‚ñ∏ (has_deriv_at_cos _).eventually_ne (neg_ne_zero.2 A)] },
  exact continuous_sin.continuous_within_at.norm.mul_at_top (norm_pos_iff.2 A)
    (tendsto_norm_nhds_within_zero.comp B).inv_tendsto_zero,
end

lemma tendsto_abs_tan_at_top (k : ‚Ñ§) :
  tendsto (Œª x, abs (tan x)) (ùìù[{(2 * k + 1) * œÄ / 2}·∂ú] ((2 * k + 1) * œÄ / 2)) at_top :=
tendsto_abs_tan_of_cos_eq_zero $ cos_eq_zero_iff.2 ‚ü®k, rfl‚ü©

@[simp] lemma continuous_at_tan {x : ‚ÑÇ} : continuous_at tan x ‚Üî cos x ‚â† 0 :=
begin
  refine ‚ü®Œª hc h‚ÇÄ, _, Œª h, (has_deriv_at_tan h).continuous_at‚ü©,
  exact not_tendsto_nhds_of_tendsto_at_top (tendsto_abs_tan_of_cos_eq_zero h‚ÇÄ) _
    (hc.norm.tendsto.mono_left inf_le_left)
end

@[simp] lemma differentiable_at_tan {x : ‚ÑÇ} : differentiable_at ‚ÑÇ tan x ‚Üî cos x ‚â† 0:=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at, Œª h, (has_deriv_at_tan h).differentiable_at‚ü©

@[simp] lemma deriv_tan (x : ‚ÑÇ) : deriv tan x = 1 / (cos x)^2 :=
if h : cos x = 0 then
  have ¬¨differentiable_at ‚ÑÇ tan x := mt differentiable_at_tan.1 (not_not.2 h),
  by simp [deriv_zero_of_not_differentiable_at this, h, sq]
else (has_deriv_at_tan h).deriv

@[simp] lemma times_cont_diff_at_tan {x : ‚ÑÇ} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚ÑÇ n tan x ‚Üî cos x ‚â† 0 :=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at,
  times_cont_diff_sin.times_cont_diff_at.div times_cont_diff_cos.times_cont_diff_at‚ü©

end complex

section log_deriv

open complex

variables {Œ± : Type*}

variables [topological_space Œ±]

variables {E : Type*} [normed_group E] [normed_space ‚ÑÇ E]

lemma has_strict_fderiv_at.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {x : E}
  (h‚ÇÅ : has_strict_fderiv_at f f' x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_strict_fderiv_at (Œª t, log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
(has_strict_deriv_at_log h‚ÇÇ).comp_has_strict_fderiv_at x h‚ÇÅ

lemma has_strict_deriv_at.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} (h‚ÇÅ : has_strict_deriv_at f f' x)
  (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_strict_deriv_at (Œª t, log (f t)) (f' / f x) x :=
by { rw div_eq_inv_mul, exact (has_strict_deriv_at_log h‚ÇÇ).comp x h‚ÇÅ }

lemma has_fderiv_at.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {x : E}
  (h‚ÇÅ : has_fderiv_at f f' x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_fderiv_at (Œª t, log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
(has_strict_deriv_at_log h‚ÇÇ).has_deriv_at.comp_has_fderiv_at x h‚ÇÅ

lemma has_deriv_at.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} (h‚ÇÅ : has_deriv_at f f' x)
  (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_at (Œª t, log (f t)) (f' / f x) x :=
by { rw div_eq_inv_mul, exact (has_strict_deriv_at_log h‚ÇÇ).has_deriv_at.comp x h‚ÇÅ }

lemma differentiable_at.clog {f : E ‚Üí ‚ÑÇ} {x : E} (h‚ÇÅ : differentiable_at ‚ÑÇ f x)
  (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable_at ‚ÑÇ (Œª t, log (f t)) x :=
(h‚ÇÅ.has_fderiv_at.clog h‚ÇÇ).differentiable_at

lemma has_fderiv_within_at.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {s : set E} {x : E}
  (h‚ÇÅ : has_fderiv_within_at f f' s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_fderiv_within_at (Œª t, log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') s x :=
(has_strict_deriv_at_log h‚ÇÇ).has_deriv_at.comp_has_fderiv_within_at x h‚ÇÅ

lemma has_deriv_within_at.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} {s : set ‚ÑÇ}
  (h‚ÇÅ : has_deriv_within_at f f' s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_within_at (Œª t, log (f t)) (f' / f x) s x :=
by { rw div_eq_inv_mul,
     exact (has_strict_deriv_at_log h‚ÇÇ).has_deriv_at.comp_has_deriv_within_at x h‚ÇÅ }

lemma differentiable_within_at.clog {f : E ‚Üí ‚ÑÇ} {s : set E} {x : E}
  (h‚ÇÅ : differentiable_within_at ‚ÑÇ f s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable_within_at ‚ÑÇ (Œª t, log (f t)) s x :=
(h‚ÇÅ.has_fderiv_within_at.clog h‚ÇÇ).differentiable_within_at

lemma differentiable_on.clog {f : E ‚Üí ‚ÑÇ} {s : set E}
  (h‚ÇÅ : differentiable_on ‚ÑÇ f s) (h‚ÇÇ : ‚àÄ x ‚àà s, 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable_on ‚ÑÇ (Œª t, log (f t)) s :=
Œª x hx, (h‚ÇÅ x hx).clog (h‚ÇÇ x hx)

lemma differentiable.clog {f : E ‚Üí ‚ÑÇ} (h‚ÇÅ : differentiable ‚ÑÇ f)
  (h‚ÇÇ : ‚àÄ x, 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable ‚ÑÇ (Œª t, log (f t)) :=
Œª x, (h‚ÇÅ x).clog (h‚ÇÇ x)

end log_deriv

namespace polynomial.chebyshev

open polynomial complex

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cos Œ∏` to the
value `cos (n * Œ∏)`. -/
lemma T_complex_cos (Œ∏ : ‚ÑÇ) :
  ‚àÄ n, (T ‚ÑÇ n).eval (cos Œ∏) = cos (n * Œ∏)
| 0       := by simp only [T_zero, eval_one, nat.cast_zero, zero_mul, cos_zero]
| 1       := by simp only [eval_X, one_mul, T_one, nat.cast_one]
| (n + 2) :=
begin
  simp only [eval_X, eval_one, T_add_two, eval_sub, eval_bit0, nat.cast_succ, eval_mul],
  rw [T_complex_cos (n + 1), T_complex_cos n],
  have aux : sin Œ∏ * sin Œ∏ = 1 - cos Œ∏ * cos Œ∏,
  { rw ‚Üê sin_sq_add_cos_sq Œ∏, ring, },
  simp only [nat.cast_add, nat.cast_one, add_mul, cos_add, one_mul, sin_add, mul_assoc, aux],
  ring,
end

/-- `cos (n * Œ∏)` is equal to the `n`-th Chebyshev polynomial of the first kind evaluated
on `cos Œ∏`. -/
lemma cos_nat_mul (n : ‚Ñï) (Œ∏ : ‚ÑÇ) :
  cos (n * Œ∏) = (T ‚ÑÇ n).eval (cos Œ∏) :=
(T_complex_cos Œ∏ n).symm

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cos Œ∏` to the
value `sin ((n+1) * Œ∏) / sin Œ∏`. -/
lemma U_complex_cos (Œ∏ : ‚ÑÇ) (n : ‚Ñï) :
  (U ‚ÑÇ n).eval (cos Œ∏) * sin Œ∏ = sin ((n+1) * Œ∏) :=
begin
  induction n with d hd,
  { simp only [U_zero, nat.cast_zero, eval_one, mul_one, zero_add, one_mul] },
  { rw U_eq_X_mul_U_add_T,
    simp only [eval_add, eval_mul, eval_X, T_complex_cos, add_mul, mul_assoc, hd, one_mul],
    conv_rhs { rw [sin_add, mul_comm] },
    push_cast,
    simp only [add_mul, one_mul] }
end

/-- `sin ((n + 1) * Œ∏)` is equal to `sin Œ∏` multiplied with the `n`-th Chebyshev polynomial of the
second kind evaluated on `cos Œ∏`. -/
lemma sin_nat_succ_mul (n : ‚Ñï) (Œ∏ : ‚ÑÇ) :
  sin ((n + 1) * Œ∏) = (U ‚ÑÇ n).eval (cos Œ∏) * sin Œ∏ :=
(U_complex_cos Œ∏ n).symm

end polynomial.chebyshev

namespace real
open_locale real

lemma has_strict_deriv_at_tan {x : ‚Ñù} (h : cos x ‚â† 0) :
  has_strict_deriv_at tan (1 / (cos x)^2) x :=
by exact_mod_cast (complex.has_strict_deriv_at_tan (by exact_mod_cast h)).real_of_complex

lemma has_deriv_at_tan {x : ‚Ñù} (h : cos x ‚â† 0) :
  has_deriv_at tan (1 / (cos x)^2) x :=
by exact_mod_cast (complex.has_deriv_at_tan (by exact_mod_cast h)).real_of_complex

lemma tendsto_abs_tan_of_cos_eq_zero {x : ‚Ñù} (hx : cos x = 0) :
  tendsto (Œª x, abs (tan x)) (ùìù[{x}·∂ú] x) at_top :=
begin
  have hx : complex.cos x = 0, by exact_mod_cast hx,
  simp only [‚Üê complex.abs_of_real, complex.of_real_tan],
  refine (complex.tendsto_abs_tan_of_cos_eq_zero hx).comp _,
  refine tendsto.inf complex.continuous_of_real.continuous_at _,
  exact tendsto_principal_principal.2 (Œª y, mt complex.of_real_inj.1)
end

lemma tendsto_abs_tan_at_top (k : ‚Ñ§) :
  tendsto (Œª x, abs (tan x)) (ùìù[{(2 * k + 1) * œÄ / 2}·∂ú] ((2 * k + 1) * œÄ / 2)) at_top :=
tendsto_abs_tan_of_cos_eq_zero $ cos_eq_zero_iff.2 ‚ü®k, rfl‚ü©

lemma continuous_at_tan {x : ‚Ñù} : continuous_at tan x ‚Üî cos x ‚â† 0 :=
begin
  refine ‚ü®Œª hc h‚ÇÄ, _, Œª h, (has_deriv_at_tan h).continuous_at‚ü©,
  exact not_tendsto_nhds_of_tendsto_at_top (tendsto_abs_tan_of_cos_eq_zero h‚ÇÄ) _
    (hc.norm.tendsto.mono_left inf_le_left)
end

lemma differentiable_at_tan {x : ‚Ñù} : differentiable_at ‚Ñù tan x ‚Üî cos x ‚â† 0 :=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at, Œª h, (has_deriv_at_tan h).differentiable_at‚ü©

@[simp] lemma deriv_tan (x : ‚Ñù) : deriv tan x = 1 / (cos x)^2 :=
if h : cos x = 0 then
  have ¬¨differentiable_at ‚Ñù tan x := mt differentiable_at_tan.1 (not_not.2 h),
  by simp [deriv_zero_of_not_differentiable_at this, h, sq]
else (has_deriv_at_tan h).deriv

@[simp] lemma times_cont_diff_at_tan {n x} : times_cont_diff_at ‚Ñù n tan x ‚Üî cos x ‚â† 0 :=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at,
  Œª h, (complex.times_cont_diff_at_tan.2 $ by exact_mod_cast h).real_of_complex‚ü©

lemma continuous_on_tan : continuous_on tan {x | cos x ‚â† 0} :=
Œª x hx, (continuous_at_tan.2 hx).continuous_within_at

@[continuity]
lemma continuous_tan : continuous (Œª x : {x | cos x ‚â† 0}, tan x) :=
continuous_on_iff_continuous_restrict.1 continuous_on_tan

lemma has_deriv_at_tan_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-(œÄ/2):‚Ñù) (œÄ/2)) :
  has_deriv_at tan (1 / (cos x)^2) x :=
has_deriv_at_tan (cos_pos_of_mem_Ioo h).ne'

lemma differentiable_at_tan_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-(œÄ/2):‚Ñù) (œÄ/2)) :
  differentiable_at ‚Ñù tan x :=
(has_deriv_at_tan_of_mem_Ioo h).differentiable_at

lemma continuous_on_tan_Ioo : continuous_on tan (Ioo (-(œÄ/2)) (œÄ/2)) :=
Œª x hx, (differentiable_at_tan_of_mem_Ioo hx).continuous_at.continuous_within_at

lemma tendsto_sin_pi_div_two : tendsto sin (ùìù[Iio (œÄ/2)] (œÄ/2)) (ùìù 1) :=
by { convert continuous_sin.continuous_within_at, simp }

lemma tendsto_cos_pi_div_two : tendsto cos (ùìù[Iio (œÄ/2)] (œÄ/2)) (ùìù[Ioi 0] 0) :=
begin
  apply tendsto_nhds_within_of_tendsto_nhds_of_eventually_within,
  { convert continuous_cos.continuous_within_at, simp },
  { filter_upwards [Ioo_mem_nhds_within_Iio (right_mem_Ioc.mpr (norm_num.lt_neg_pos
      _ _ pi_div_two_pos pi_div_two_pos))] Œª x hx, cos_pos_of_mem_Ioo hx },
end

lemma tendsto_tan_pi_div_two : tendsto tan (ùìù[Iio (œÄ/2)] (œÄ/2)) at_top :=
begin
  convert tendsto_cos_pi_div_two.inv_tendsto_zero.at_top_mul zero_lt_one
            tendsto_sin_pi_div_two,
  simp only [pi.inv_apply, ‚Üê div_eq_inv_mul, ‚Üê tan_eq_sin_div_cos]
end

lemma tendsto_sin_neg_pi_div_two : tendsto sin (ùìù[Ioi (-(œÄ/2))] (-(œÄ/2))) (ùìù (-1)) :=
by { convert continuous_sin.continuous_within_at, simp }

lemma tendsto_cos_neg_pi_div_two : tendsto cos (ùìù[Ioi (-(œÄ/2))] (-(œÄ/2))) (ùìù[Ioi 0] 0) :=
begin
  apply tendsto_nhds_within_of_tendsto_nhds_of_eventually_within,
  { convert continuous_cos.continuous_within_at, simp },
  { filter_upwards [Ioo_mem_nhds_within_Ioi (left_mem_Ico.mpr (norm_num.lt_neg_pos
      _ _ pi_div_two_pos pi_div_two_pos))] Œª x hx, cos_pos_of_mem_Ioo hx },
end

lemma tendsto_tan_neg_pi_div_two : tendsto tan (ùìù[Ioi (-(œÄ/2))] (-(œÄ/2))) at_bot :=
begin
  convert tendsto_cos_neg_pi_div_two.inv_tendsto_zero.at_top_mul_neg (by norm_num)
            tendsto_sin_neg_pi_div_two,
  simp only [pi.inv_apply, ‚Üê div_eq_inv_mul, ‚Üê tan_eq_sin_div_cos]
end

lemma surj_on_tan : surj_on tan (Ioo (-(œÄ / 2)) (œÄ / 2)) univ :=
have _ := neg_lt_self pi_div_two_pos,
continuous_on_tan_Ioo.surj_on_of_tendsto (nonempty_Ioo.2 this)
  (by simp [tendsto_tan_neg_pi_div_two, this]) (by simp [tendsto_tan_pi_div_two, this])

lemma tan_surjective : function.surjective tan :=
Œª x, surj_on_tan.subset_range trivial

lemma image_tan_Ioo : tan '' (Ioo (-(œÄ / 2)) (œÄ / 2)) = univ :=
univ_subset_iff.1 surj_on_tan

/-- `real.tan` as an `order_iso` between `(-(œÄ / 2), œÄ / 2)` and `‚Ñù`. -/
def tan_order_iso : Ioo (-(œÄ / 2)) (œÄ / 2) ‚âÉo ‚Ñù :=
(strict_mono_incr_on_tan.order_iso _ _).trans $ (order_iso.set_congr _ _ image_tan_Ioo).trans
  order_iso.set.univ

/-- Inverse of the `tan` function, returns values in the range `-œÄ / 2 < arctan x` and
`arctan x < œÄ / 2` -/
@[pp_nodot] noncomputable def arctan (x : ‚Ñù) : ‚Ñù :=
tan_order_iso.symm x

@[simp] lemma tan_arctan (x : ‚Ñù) : tan (arctan x) = x :=
tan_order_iso.apply_symm_apply x

lemma arctan_mem_Ioo (x : ‚Ñù) : arctan x ‚àà Ioo (-(œÄ / 2)) (œÄ / 2) :=
subtype.coe_prop _

lemma arctan_tan {x : ‚Ñù} (hx‚ÇÅ : -(œÄ / 2) < x) (hx‚ÇÇ : x < œÄ / 2) : arctan (tan x) = x :=
subtype.ext_iff.1 $ tan_order_iso.symm_apply_apply ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü©

lemma cos_arctan_pos (x : ‚Ñù) : 0 < cos (arctan x) :=
cos_pos_of_mem_Ioo $ arctan_mem_Ioo x

lemma cos_sq_arctan (x : ‚Ñù) : cos (arctan x) ^ 2 = 1 / (1 + x ^ 2) :=
by rw [one_div, ‚Üê inv_one_add_tan_sq (cos_arctan_pos x).ne', tan_arctan]

lemma sin_arctan (x : ‚Ñù) : sin (arctan x) = x / sqrt (1 + x ^ 2) :=
by rw [‚Üê tan_div_sqrt_one_add_tan_sq (cos_arctan_pos x), tan_arctan]

lemma cos_arctan (x : ‚Ñù) : cos (arctan x) = 1 / sqrt (1 + x ^ 2) :=
by rw [one_div, ‚Üê inv_sqrt_one_add_tan_sq (cos_arctan_pos x), tan_arctan]

lemma arctan_lt_pi_div_two (x : ‚Ñù) : arctan x < œÄ / 2 :=
(arctan_mem_Ioo x).2

lemma neg_pi_div_two_lt_arctan (x : ‚Ñù) : -(œÄ / 2) < arctan x :=
(arctan_mem_Ioo x).1

lemma arctan_eq_arcsin (x : ‚Ñù) : arctan x = arcsin (x / sqrt (1 + x ^ 2)) :=
eq.symm $ arcsin_eq_of_sin_eq (sin_arctan x) (mem_Icc_of_Ioo $ arctan_mem_Ioo x)

lemma arcsin_eq_arctan {x : ‚Ñù} (h : x ‚àà Ioo (-(1:‚Ñù)) 1) :
  arcsin x = arctan (x / sqrt (1 - x ^ 2)) :=
begin
  rw [arctan_eq_arcsin, div_pow, sq_sqrt, one_add_div, div_div_eq_div_mul,
      ‚Üê sqrt_mul, mul_div_cancel', sub_add_cancel, sqrt_one, div_one];
  nlinarith [h.1, h.2],
end

@[simp] lemma arctan_zero : arctan 0 = 0 :=
by simp [arctan_eq_arcsin]

lemma arctan_eq_of_tan_eq {x y : ‚Ñù} (h : tan x = y) (hx : x ‚àà Ioo (-(œÄ / 2)) (œÄ / 2)) :
  arctan y = x :=
inj_on_tan (arctan_mem_Ioo _) hx (by rw [tan_arctan, h])

@[simp] lemma arctan_one : arctan 1 = œÄ / 4 :=
arctan_eq_of_tan_eq tan_pi_div_four $ by split; linarith [pi_pos]

@[simp] lemma arctan_neg (x : ‚Ñù) : arctan (-x) = - arctan x :=
by simp [arctan_eq_arcsin, neg_div]

@[continuity]
lemma continuous_arctan : continuous arctan :=
continuous_subtype_coe.comp tan_order_iso.to_homeomorph.continuous_inv_fun

lemma continuous_at_arctan {x : ‚Ñù} : continuous_at arctan x := continuous_arctan.continuous_at

/-- `real.tan` as a `local_homeomorph` between `(-(œÄ / 2), œÄ / 2)` and the whole line. -/
def tan_local_homeomorph : local_homeomorph ‚Ñù ‚Ñù :=
{ to_fun := tan,
  inv_fun := arctan,
  source := Ioo (-(œÄ / 2)) (œÄ / 2),
  target := univ,
  map_source' := maps_to_univ _ _,
  map_target' := Œª y hy, arctan_mem_Ioo y,
  left_inv' := Œª x hx, arctan_tan hx.1 hx.2,
  right_inv' := Œª y hy, tan_arctan y,
  open_source := is_open_Ioo,
  open_target := is_open_univ,
  continuous_to_fun := continuous_on_tan_Ioo,
  continuous_inv_fun := continuous_arctan.continuous_on }

@[simp] lemma coe_tan_local_homeomorph : ‚áëtan_local_homeomorph = tan := rfl
@[simp] lemma coe_tan_local_homeomorph_symm : ‚áëtan_local_homeomorph.symm = arctan := rfl

lemma has_strict_deriv_at_arctan (x : ‚Ñù) : has_strict_deriv_at arctan (1 / (1 + x^2)) x :=
have A : cos (arctan x) ‚â† 0 := (cos_arctan_pos x).ne',
by simpa [cos_sq_arctan]
  using tan_local_homeomorph.has_strict_deriv_at_symm trivial (by simpa) (has_strict_deriv_at_tan A)

lemma has_deriv_at_arctan (x : ‚Ñù) : has_deriv_at arctan (1 / (1 + x^2)) x :=
(has_strict_deriv_at_arctan x).has_deriv_at

lemma differentiable_at_arctan (x : ‚Ñù) : differentiable_at ‚Ñù arctan x :=
(has_deriv_at_arctan x).differentiable_at

lemma differentiable_arctan : differentiable ‚Ñù arctan := differentiable_at_arctan

@[simp] lemma deriv_arctan : deriv arctan = (Œª x, 1 / (1 + x^2)) :=
funext $ Œª x, (has_deriv_at_arctan x).deriv

lemma times_cont_diff_arctan {n : with_top ‚Ñï} : times_cont_diff ‚Ñù n arctan :=
times_cont_diff_iff_times_cont_diff_at.2 $ Œª x,
have cos (arctan x) ‚â† 0 := (cos_arctan_pos x).ne',
tan_local_homeomorph.times_cont_diff_at_symm_deriv (by simpa) trivial (has_deriv_at_tan this)
  (times_cont_diff_at_tan.2 this)

end real

section
/-!
### Lemmas for derivatives of the composition of `real.arctan` with a differentiable function

In this section we register lemmas for the derivatives of the composition of `real.arctan` with a
differentiable function, for standalone use and use with `simp`. -/

open real

section deriv

variables {f : ‚Ñù ‚Üí ‚Ñù} {f' x : ‚Ñù} {s : set ‚Ñù}

lemma has_strict_deriv_at.arctan (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) * f') x :=
(real.has_strict_deriv_at_arctan (f x)).comp x hf

lemma has_deriv_at.arctan (hf : has_deriv_at f f' x) :
  has_deriv_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) * f') x :=
(real.has_deriv_at_arctan (f x)).comp x hf

lemma has_deriv_within_at.arctan (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) * f') s x :=
(real.has_deriv_at_arctan (f x)).comp_has_deriv_within_at x hf

lemma deriv_within_arctan (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œª x, arctan (f x)) s x = (1 / (1 + (f x)^2)) * (deriv_within f s x) :=
hf.has_deriv_within_at.arctan.deriv_within hxs

@[simp] lemma deriv_arctan (hc : differentiable_at ‚Ñù f x) :
  deriv (Œª x, arctan (f x)) x = (1 / (1 + (f x)^2)) * (deriv f x) :=
hc.has_deriv_at.arctan.deriv

end deriv

section fderiv

variables {E : Type*} [normed_group E] [normed_space ‚Ñù E] {f : E ‚Üí ‚Ñù} {f' : E ‚ÜíL[‚Ñù] ‚Ñù} {x : E}
  {s : set E} {n : with_top ‚Ñï}

lemma has_strict_fderiv_at.arctan (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) ‚Ä¢ f') x :=
(has_strict_deriv_at_arctan (f x)).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.arctan (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) ‚Ä¢ f') x :=
(has_deriv_at_arctan (f x)).comp_has_fderiv_at x hf

lemma has_fderiv_within_at.arctan (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Œª x, arctan (f x)) ((1 / (1 + (f x)^2)) ‚Ä¢ f') s x :=
(has_deriv_at_arctan (f x)).comp_has_fderiv_within_at x hf

lemma fderiv_within_arctan (hf : differentiable_within_at ‚Ñù f s x)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  fderiv_within ‚Ñù (Œª x, arctan (f x)) s x = (1 / (1 + (f x)^2)) ‚Ä¢ (fderiv_within ‚Ñù f s x) :=
hf.has_fderiv_within_at.arctan.fderiv_within hxs

@[simp] lemma fderiv_arctan (hc : differentiable_at ‚Ñù f x) :
  fderiv ‚Ñù (Œª x, arctan (f x)) x = (1 / (1 + (f x)^2)) ‚Ä¢ (fderiv ‚Ñù f x) :=
hc.has_fderiv_at.arctan.fderiv

lemma differentiable_within_at.arctan (hf : differentiable_within_at ‚Ñù f s x) :
  differentiable_within_at ‚Ñù (Œª x, real.arctan (f x)) s x :=
hf.has_fderiv_within_at.arctan.differentiable_within_at

@[simp] lemma differentiable_at.arctan (hc : differentiable_at ‚Ñù f x) :
  differentiable_at ‚Ñù (Œª x, arctan (f x)) x :=
hc.has_fderiv_at.arctan.differentiable_at

lemma differentiable_on.arctan (hc : differentiable_on ‚Ñù f s) :
  differentiable_on ‚Ñù (Œª x, arctan (f x)) s :=
Œª x h, (hc x h).arctan

@[simp] lemma differentiable.arctan (hc : differentiable ‚Ñù f) :
  differentiable ‚Ñù (Œª x, arctan (f x)) :=
Œª x, (hc x).arctan

lemma times_cont_diff_at.arctan (h : times_cont_diff_at ‚Ñù n f x) :
  times_cont_diff_at ‚Ñù n (Œª x, arctan (f x)) x :=
times_cont_diff_arctan.times_cont_diff_at.comp x h

lemma times_cont_diff.arctan (h : times_cont_diff ‚Ñù n f) :
  times_cont_diff ‚Ñù n (Œª x, arctan (f x)) :=
times_cont_diff_arctan.comp h

lemma times_cont_diff_within_at.arctan (h : times_cont_diff_within_at ‚Ñù n f s x) :
  times_cont_diff_within_at ‚Ñù n (Œª x, arctan (f x)) s x :=
times_cont_diff_arctan.comp_times_cont_diff_within_at h

lemma times_cont_diff_on.arctan (h : times_cont_diff_on ‚Ñù n f s) :
  times_cont_diff_on ‚Ñù n (Œª x, arctan (f x)) s :=
times_cont_diff_arctan.comp_times_cont_diff_on h

end fderiv
end
