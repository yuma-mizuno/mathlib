/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.metric_space.basic
import measure_theory.constructions.borel_space

/-!
# Vitali families

On a metric space with a measure `Î¼`, consider for each `x` a family of closed sets with
nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the following
property: consider a (possible non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file defines Vitali families.
-/

open measure_theory metric set filter topological_space
open_locale filter ennreal

variables {Î± : Type*} [metric_space Î±]

/-- On a metric space with a measure `Î¼`, consider for each `x` a family of closed sets with
nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
@[nolint has_inhabited_instance]
structure vitali_family {m : measurable_space Î±} (Î¼ : measure Î±) :=
(sets_at : Î  (x : Î±), set (set Î±))
(center_mem : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ x âˆˆ y)
(is_closed : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ is_closed y)
(nonempty_interior : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ (interior y).nonempty)
(nontrivial : âˆ€ (x : Î±) (Îµ > (0 : â„)), âˆƒ y âˆˆ sets_at x, y âŠ† closed_ball x Îµ)
(covering : âˆ€ (s : set Î±) (f : Î  (x : Î±), set (set Î±)), (âˆ€ x âˆˆ s, f x âŠ† sets_at x) â†’
  (âˆ€ (x âˆˆ s) (Îµ > (0 : â„)), âˆƒ a âˆˆ f x, a âŠ† closed_ball x Îµ) â†’
  âˆƒ (t : set Î±) (u : Î± â†’ set Î±), t âŠ† s âˆ§ pairwise_on t (disjoint on u) âˆ§ (âˆ€ x âˆˆ t, u x âˆˆ f x)
  âˆ§ Î¼ (s \ â‹ƒ x âˆˆ t, u x) = 0)

namespace vitali_family

variables {m : measurable_space Î±} {Î¼ : measure Î±}

structure fine_subfamily_on (v : vitali_family Î¼) (f : Î± â†’ set (set Î±)) (s : set Î±) :=
(subset : âˆ€ x âˆˆ s, f x âŠ† v.sets_at x)
(fine : âˆ€ x âˆˆ s, âˆ€ (Îµ > 0), âˆƒ a âˆˆ f x, a âŠ† closed_ball x Îµ)

namespace fine_subfamily_on

variables {v : vitali_family Î¼} {f : Î± â†’ set (set Î±)} {s : set Î±} (h : v.fine_subfamily_on f s)
include h

theorem exists_disjoint_covering_ae :
  âˆƒ (t : set Î±) (u : Î± â†’ set Î±), t âŠ† s âˆ§ pairwise_on t (disjoint on u) âˆ§ (âˆ€ x âˆˆ t, u x âˆˆ f x)
  âˆ§ Î¼ (s \ â‹ƒ x âˆˆ t, u x) = 0 :=
v.covering s f h.subset h.fine

protected def t : set Î± :=
h.exists_disjoint_covering_ae.some

protected def u : Î± â†’ set Î± :=
h.exists_disjoint_covering_ae.some_spec.some

lemma t_subset_s : h.t âŠ† s :=
h.exists_disjoint_covering_ae.some_spec.some_spec.1

lemma u_disjoint : pairwise_on h.t (disjoint on h.u) :=
h.exists_disjoint_covering_ae.some_spec.some_spec.2.1

lemma u_mem : âˆ€ x âˆˆ h.t, h.u x âˆˆ f x :=
h.exists_disjoint_covering_ae.some_spec.some_spec.2.2.1

lemma measure_diff_bUnion : Î¼ (s \ â‹ƒ x âˆˆ h.t, h.u x) = 0 :=
h.exists_disjoint_covering_ae.some_spec.some_spec.2.2.2

lemma t_countable [second_countable_topology Î±] : countable h.t :=
begin
  apply countable_of_nonempty_interior_of_disjoint h.u (Î» x hx, _) h.u_disjoint,
  exact v.nonempty_interior _ _ (h.subset x (h.t_subset_s hx) (h.u_mem x hx)),
end

protected lemma is_closed_u {x : Î±} (hx : x âˆˆ h.t) : _root_.is_closed (h.u x) :=
v.is_closed x _ (h.subset x (h.t_subset_s hx) (h.u_mem x hx))

end fine_subfamily_on

variable (v : vitali_family Î¼)

def filter_at (x : Î±) : filter (set Î±) :=
â¨… (Îµ âˆˆ Ioi (0 : â„)), ğ“Ÿ {a âˆˆ v.sets_at x | a âŠ† closed_ball x Îµ}

lemma mem_filter_at_iff {x : Î±} {s : set (set Î±)} :
  (s âˆˆ v.filter_at x) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ a âˆˆ s :=
begin
  simp only [filter_at, exists_prop, gt_iff_lt],
  rw mem_binfi_of_directed,
  { simp only [subset_def, and_imp, exists_prop, mem_sep_eq, mem_Ioi, mem_principal] },
  { simp only [directed_on, exists_prop, ge_iff_le, le_principal_iff, mem_Ioi, order.preimage,
      mem_principal],
    assume x hx y hy,
    refine âŸ¨min x y, lt_min hx hy,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_left _ _))âŸ©,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_right _ _))âŸ©âŸ© },
  { exact âŸ¨(1 : â„), mem_Ioi.2 zero_lt_oneâŸ© }
end

lemma eventually_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆ€á¶  a in v.filter_at x, P a) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ P a :=
v.mem_filter_at_iff

lemma frequently_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆƒá¶  a in v.filter_at x, P a) â†” âˆ€ (Îµ > (0 : â„)), âˆƒ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ âˆ§ P a :=
by simp only [filter.frequently, eventually_filter_at_iff, not_exists, exists_prop, not_and,
  not_not, not_forall]

theorem ae_eventually_measure_pos [second_countable_topology Î±] [opens_measurable_space Î±] :
  âˆ€áµ x âˆ‚Î¼, âˆ€á¶  a in v.filter_at x, 0 < Î¼ a :=
begin
  set s := {x | Â¬ (âˆ€á¶  a in v.filter_at x, 0 < Î¼ a)} with hs,
  simp only [not_lt, not_eventually, nonpos_iff_eq_zero] at hs,
  change Î¼ s = 0,
  let f : Î± â†’ set (set Î±) := Î» x, {a âˆˆ v.sets_at x | Î¼ a = 0},
  have h : v.fine_subfamily_on f s,
  { refine âŸ¨Î» x hx y hy, hy.1, Î» x hx Îµ Îµpos, _âŸ©,
    rw hs at hx,
    simp only [frequently_filter_at_iff, exists_prop, gt_iff_lt, mem_set_of_eq] at hx,
    rcases hx Îµ Îµpos with âŸ¨a, a_sets, ax, Î¼aâŸ©,
    exact âŸ¨a, âŸ¨a_sets, Î¼aâŸ©, axâŸ© },
  refine le_antisymm _ bot_le,
  calc Î¼ s â‰¤ Î¼ ((s \ â‹ƒ (x âˆˆ h.t), h.u x) âˆª (â‹ƒ (x âˆˆ h.t), h.u x)) :
    measure_mono (by simp only [subset_union_left, diff_union_self])
  ... â‰¤ Î¼ (s \ â‹ƒ (x âˆˆ h.t), h.u x) + Î¼ (â‹ƒ (x âˆˆ h.t), h.u x) : measure_union_le _ _
  ... = 0 + âˆ‘' (x : h.t), Î¼ (h.u x) : by rw [h.measure_diff_bUnion,
    measure_bUnion h.t_countable h.u_disjoint (Î» x hx, (h.is_closed_u hx).measurable_set)]
  ... = 0 + âˆ‘' (x : h.t), 0 : by { congr, ext1 x, exact (h.u_mem x x.2).2 }
  ... = 0 : by simp only [tsum_zero, add_zero]
end

theorem eventually_measure_lt_top [is_locally_finite_measure Î¼] (x : Î±) :
  âˆ€á¶  a in v.filter_at x, Î¼ a < âˆ :=
begin
  obtain âŸ¨Îµ, Îµpos, Î¼ÎµâŸ© : âˆƒ (Îµ : â„) (hi : 0 < Îµ), Î¼ (closed_ball x Îµ) < âˆ :=
    (Î¼.finite_at_nhds x).exists_mem_basis nhds_basis_closed_ball,
  exact v.eventually_filter_at_iff.2 âŸ¨Îµ, Îµpos, Î» a ha haÎµ, (measure_mono haÎµ).trans_lt Î¼ÎµâŸ©,
end

end vitali_family
