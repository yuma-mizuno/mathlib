/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.metric_space.basic
import measure_theory.measure.regular

/-!
# Vitali families

On a metric space with a measure `Î¼`, consider for each `x` a family of closed sets with
nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the following
property: consider a (possible non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file defines Vitali families and proves its basic properties.
-/

open measure_theory metric set filter topological_space
open_locale filter ennreal measure_theory nnreal topological_space

local attribute [instance] emetric.second_countable_of_sigma_compact

variables {Î± : Type*} [metric_space Î±]

/-- On a metric space with a measure `Î¼`, consider for each `x` a family of closed sets with
nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
@[nolint has_inhabited_instance]
structure vitali_family {m : measurable_space Î±} (Î¼ : measure Î±) :=
(sets_at : Î  (x : Î±), set (set Î±))
(center_mem : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ x âˆˆ y)
(is_closed : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ is_closed y)
(nonempty_interior : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ (interior y).nonempty)
(nontrivial : âˆ€ (x : Î±) (Îµ > (0 : â„)), âˆƒ y âˆˆ sets_at x, y âŠ† closed_ball x Îµ)
(covering : âˆ€ (s : set Î±) (f : Î  (x : Î±), set (set Î±)), (âˆ€ x âˆˆ s, f x âŠ† sets_at x) â†’
  (âˆ€ (x âˆˆ s) (Îµ > (0 : â„)), âˆƒ a âˆˆ f x, a âŠ† closed_ball x Îµ) â†’
  âˆƒ (t : set Î±) (u : Î± â†’ set Î±), t âŠ† s âˆ§ pairwise_on t (disjoint on u) âˆ§ (âˆ€ x âˆˆ t, u x âˆˆ f x)
  âˆ§ Î¼ (s \ â‹ƒ x âˆˆ t, u x) = 0)

namespace vitali_family

variables {m : measurable_space Î±} {Î¼ : measure Î±}
include Î¼

/-- A Vitali family for a measure `Î¼` is also a Vitali family for any measure absolutely continuous
with respect to `Î¼`. -/
def mono (v : vitali_family Î¼) (Î½ : measure Î±) (hÎ½ : Î½ â‰ª Î¼) :
  vitali_family Î½ :=
{ sets_at := v.sets_at,
  center_mem := v.center_mem,
  is_closed := v.is_closed,
  nonempty_interior := v.nonempty_interior,
  nontrivial := v.nontrivial,
  covering := Î» s f h h', begin
    rcases v.covering s f h h' with âŸ¨t, u, ts, u_disj, uf, Î¼uâŸ©,
    exact âŸ¨t, u, ts, u_disj, uf, hÎ½ Î¼uâŸ©
  end }

/-- Given a Vitali family `v` for a measure `Î¼`, a family `f` is a fine subfamily on a set `s` if
every point `x` in `s` belongs to arbitrarily small sets in `v.sets_at x âˆ© f x`. This is precisely
the subfamilies for which the Vitali family definition ensures that one can extract a disjoint
covering of almost all `s`. -/
def fine_subfamily_on (v : vitali_family Î¼) (f : Î± â†’ set (set Î±)) (s : set Î±) : Prop :=
âˆ€ x âˆˆ s, âˆ€ (Îµ > 0), âˆƒ a âˆˆ v.sets_at x âˆ© f x, a âŠ† closed_ball x Îµ

namespace fine_subfamily_on

variables {v : vitali_family Î¼} {f : Î± â†’ set (set Î±)} {s : set Î±} (h : v.fine_subfamily_on f s)
include h

theorem exists_disjoint_covering_ae :
  âˆƒ (t : set Î±) (u : Î± â†’ set Î±), t âŠ† s âˆ§ pairwise_on t (disjoint on u) âˆ§
    (âˆ€ x âˆˆ t, u x âˆˆ v.sets_at x âˆ© f x) âˆ§ Î¼ (s \ â‹ƒ x âˆˆ t, u x) = 0 :=
v.covering s (Î» x, v.sets_at x âˆ© f x) (Î» x hx, inter_subset_left _ _) h

/-- Given `h : v.fine_subfamily_on f s`, then `h.t` is a subset of `s` parametrizing a disjoint
covering of almost every `s`. -/
protected def t : set Î± :=
h.exists_disjoint_covering_ae.some

/-- Given `h : v.fine_subfamily_on f s`, then `h.u x` is a set in the family, for `x âˆˆ h.t`, such
that these sets form a disjoint covering of almost every `s`. -/
protected def u : Î± â†’ set Î± :=
h.exists_disjoint_covering_ae.some_spec.some

lemma t_subset_s : h.t âŠ† s :=
h.exists_disjoint_covering_ae.some_spec.some_spec.1

lemma u_disjoint : pairwise_on h.t (disjoint on h.u) :=
h.exists_disjoint_covering_ae.some_spec.some_spec.2.1

lemma u_disjoint_subtype : pairwise (disjoint on (Î» x : h.t, h.u x)) :=
(pairwise_subtype_iff_pairwise_on _ _).2 h.u_disjoint

lemma u_mem_f {x : Î±} (hx : x âˆˆ h.t) : h.u x âˆˆ f x :=
(h.exists_disjoint_covering_ae.some_spec.some_spec.2.2.1 x hx).2

lemma u_mem_v {x : Î±} (hx : x âˆˆ h.t) : h.u x âˆˆ v.sets_at x :=
(h.exists_disjoint_covering_ae.some_spec.some_spec.2.2.1 x hx).1

lemma measure_diff_bUnion : Î¼ (s \ â‹ƒ x âˆˆ h.t, h.u x) = 0 :=
h.exists_disjoint_covering_ae.some_spec.some_spec.2.2.2

lemma t_countable [second_countable_topology Î±] : countable h.t :=
countable_of_nonempty_interior_of_disjoint h.u (Î» x hx, v.nonempty_interior _ _ (h.u_mem_v hx))
  h.u_disjoint

noncomputable instance [second_countable_topology Î±] : encodable h.t :=
h.t_countable.to_encodable

protected lemma is_closed_u {x : Î±} (hx : x âˆˆ h.t) : _root_.is_closed (h.u x) :=
v.is_closed x _ (h.u_mem_v hx)

lemma measure_le_tsum_of_absolutely_continuous
  [second_countable_topology Î±] [opens_measurable_space Î±]
  {Ï : measure Î±} (hÏ : Ï â‰ª Î¼) :
  Ï s â‰¤ âˆ‘' (x : h.t), Ï (h.u x) :=
calc Ï s â‰¤ Ï ((s \ â‹ƒ (x âˆˆ h.t), h.u x) âˆª (â‹ƒ (x âˆˆ h.t), h.u x)) :
    measure_mono (by simp only [subset_union_left, diff_union_self])
  ... â‰¤ Ï (s \ â‹ƒ (x âˆˆ h.t), h.u x) + Ï (â‹ƒ (x âˆˆ h.t), h.u x) : measure_union_le _ _
  ... = âˆ‘' (x : h.t), Ï (h.u x) : by rw [hÏ h.measure_diff_bUnion,
    measure_bUnion h.t_countable h.u_disjoint (Î» x hx, (h.is_closed_u hx).measurable_set), zero_add]

lemma measure_le_tsum [second_countable_topology Î±] [opens_measurable_space Î±] :
  Î¼ s â‰¤ âˆ‘' (x : h.t), Î¼ (h.u x) :=
h.measure_le_tsum_of_absolutely_continuous measure.absolutely_continuous.rfl

end fine_subfamily_on

variable (v : vitali_family Î¼)
include v

/-- Given a vitali family `v`, then `v.filter_at x` is the filter on `set Î±` made of those families
that contain all sets of `v.sets_at x` of a sufficiently small diameter. This filter makes it
possible to express limiting behavior when sets in `v.sets_at x` shrink to `x`. -/
def filter_at (x : Î±) : filter (set Î±) :=
â¨… (Îµ âˆˆ Ioi (0 : â„)), ğ“Ÿ {a âˆˆ v.sets_at x | a âŠ† closed_ball x Îµ}

lemma mem_filter_at_iff {x : Î±} {s : set (set Î±)} :
  (s âˆˆ v.filter_at x) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ a âˆˆ s :=
begin
  simp only [filter_at, exists_prop, gt_iff_lt],
  rw mem_binfi_of_directed,
  { simp only [subset_def, and_imp, exists_prop, mem_sep_eq, mem_Ioi, mem_principal] },
  { simp only [directed_on, exists_prop, ge_iff_le, le_principal_iff, mem_Ioi, order.preimage,
      mem_principal],
    assume x hx y hy,
    refine âŸ¨min x y, lt_min hx hy,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_left _ _))âŸ©,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_right _ _))âŸ©âŸ© },
  { exact âŸ¨(1 : â„), mem_Ioi.2 zero_lt_oneâŸ© }
end

lemma eventually_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆ€á¶  a in v.filter_at x, P a) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ P a :=
v.mem_filter_at_iff

lemma eventually_filter_at_mem_sets (x : Î±) :
  âˆ€á¶  a in v.filter_at x, a âˆˆ v.sets_at x :=
begin
  simp only [eventually_filter_at_iff, exists_prop, and_true, gt_iff_lt,
             implies_true_iff] {contextual := tt},
  exact âŸ¨1, zero_lt_oneâŸ©
end

lemma frequently_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆƒá¶  a in v.filter_at x, P a) â†” âˆ€ (Îµ > (0 : â„)), âˆƒ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ âˆ§ P a :=
by simp only [filter.frequently, eventually_filter_at_iff, not_exists, exists_prop, not_and,
  not_not, not_forall]

lemma eventually_filter_at_subset_of_nhds {x : Î±} {o : set Î±} (hx : o âˆˆ ğ“ x) :
  âˆ€á¶  a in v.filter_at x, a âŠ† o :=
begin
  rw eventually_filter_at_iff,
  rcases metric.mem_nhds_iff.1 hx with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  exact âŸ¨Îµ/2, half_pos Îµpos,
    Î» a av ha, ha.trans ((closed_ball_subset_ball (half_lt_self Îµpos)).trans hÎµ)âŸ©
end

lemma fine_subfamily_on_of_frequently (v : vitali_family Î¼) (f : Î± â†’ set (set Î±)) (s : set Î±)
  (h : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filter_at x, a âˆˆ f x) :
  v.fine_subfamily_on f s :=
begin
  assume x hx Îµ Îµpos,
  obtain âŸ¨a, av, ha, afâŸ© : âˆƒ (a : set Î±) (H : a âˆˆ v.sets_at x), a âŠ† closed_ball x Îµ âˆ§ a âˆˆ f x :=
    v.frequently_filter_at_iff.1 (h x hx) Îµ Îµpos,
  exact âŸ¨a, âŸ¨av, afâŸ©, haâŸ©,
end

/-- For almost every point `x`, sufficiently small sets in a Vitali family around `x` have positive
measure. (This is a nontrivial result, following from the covering property of Vitali families). -/
theorem ae_eventually_measure_pos [second_countable_topology Î±] [opens_measurable_space Î±] :
  âˆ€áµ x âˆ‚Î¼, âˆ€á¶  a in v.filter_at x, 0 < Î¼ a :=
begin
  set s := {x | Â¬ (âˆ€á¶  a in v.filter_at x, 0 < Î¼ a)} with hs,
  simp only [not_lt, not_eventually, nonpos_iff_eq_zero] at hs,
  change Î¼ s = 0,
  let f : Î± â†’ set (set Î±) := Î» x, {a | Î¼ a = 0},
  have h : v.fine_subfamily_on f s,
  { assume x hx Îµ Îµpos,
    rw hs at hx,
    simp only [frequently_filter_at_iff, exists_prop, gt_iff_lt, mem_set_of_eq] at hx,
    rcases hx Îµ Îµpos with âŸ¨a, a_sets, ax, Î¼aâŸ©,
    exact âŸ¨a, âŸ¨a_sets, Î¼aâŸ©, axâŸ© },
  refine le_antisymm _ bot_le,
  calc Î¼ s â‰¤ âˆ‘' (x : h.t), Î¼ (h.u x) : h.measure_le_tsum
  ... = âˆ‘' (x : h.t), 0 : by { congr, ext1 x, exact h.u_mem_f x.2 }
  ... = 0 : by simp only [tsum_zero, add_zero]
end

/-- For every point `x`, sufficiently small sets in a Vitali family around `x` have finite measure.
(This is a trivial result, following from the fact that the measure is locally finite). -/
theorem eventually_measure_lt_top [is_locally_finite_measure Î¼] (x : Î±) :
  âˆ€á¶  a in v.filter_at x, Î¼ a < âˆ :=
begin
  obtain âŸ¨Îµ, Îµpos, Î¼ÎµâŸ© : âˆƒ (Îµ : â„) (hi : 0 < Îµ), Î¼ (closed_ball x Îµ) < âˆ :=
    (Î¼.finite_at_nhds x).exists_mem_basis nhds_basis_closed_ball,
  exact v.eventually_filter_at_iff.2 âŸ¨Îµ, Îµpos, Î» a ha haÎµ, (measure_mono haÎµ).trans_lt Î¼ÎµâŸ©,
end

/-- If two measures `Ï` and `Î½` have, at every point of a set `s`, arbitrarily small sets in a
Vitali family satisfying `Ï a â‰¤ Î½ a`, then `Ï s â‰¤ Î½ s` if `Ï â‰ª Î¼`.-/
theorem measure_le_of_frequently_le [sigma_compact_space Î±] [borel_space Î±]
  {Ï : measure Î±} (Î½ : measure Î±) [is_locally_finite_measure Î½]
  (hÏ : Ï â‰ª Î¼) (s : set Î±) (hs : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filter_at x, Ï a â‰¤ Î½ a) :
  Ï s â‰¤ Î½ s :=
begin
  -- this follows from a covering argument using the sets satisfying `Ï a â‰¤ Î½ a`.
  apply ennreal.le_of_forall_pos_le_add (Î» Îµ Îµpos hc, _),
  obtain âŸ¨U, sU, U_open, Î½UâŸ© : âˆƒ (U : set Î±) (H : s âŠ† U), is_open U âˆ§ Î½ U â‰¤ Î½ s + Îµ :=
    exists_is_open_le_add s Î½ (ennreal.coe_pos.2 Îµpos).ne',
  let f : Î± â†’ set (set Î±) := Î» x, {a | Ï a â‰¤ Î½ a âˆ§ a âŠ† U},
  have h : v.fine_subfamily_on f s,
  { apply v.fine_subfamily_on_of_frequently f s (Î» x hx, _),
    have := (hs x hx).and_eventually ((v.eventually_filter_at_mem_sets x).and
      (v.eventually_filter_at_subset_of_nhds (U_open.mem_nhds (sU hx)))),
    apply frequently.mono this,
    rintros a âŸ¨Ïa, av, aUâŸ©,
    exact âŸ¨Ïa, aUâŸ© },
  calc Ï s â‰¤ âˆ‘' (x : h.t), Ï (h.u x) : h.measure_le_tsum_of_absolutely_continuous hÏ
  ... â‰¤ âˆ‘' (x : h.t), Î½ (h.u x) : ennreal.tsum_le_tsum (Î» x, (h.u_mem_f x.2).1)
  ... = Î½ (â‹ƒ (x : h.t), h.u x) :
    by rw [measure_Union h.u_disjoint_subtype (Î» i, (h.is_closed_u i.2).measurable_set)]
  ... â‰¤ Î½ U : measure_mono (Union_subset (Î» i, (h.u_mem_f i.2).2))
  ... â‰¤ Î½ s + Îµ : Î½U
end

/-- If a measure `Ï` is singular with respect to `Î¼`, then for `Î¼` almost every `x`, the ratio
`Ï a / Î¼ a` tends to zero when `a` shrinks to `x` along the Vitali family. This makes sense
as `Î¼ a` is eventually positive by `ae_eventually_measure_pos`. -/
lemma ae_eventually_measure_zero_of_singular [sigma_compact_space Î±] [borel_space Î±]
  {Ï : measure Î±} (hÏ : Ï âŠ¥â‚˜ Î¼) [is_locally_finite_measure Ï] [is_locally_finite_measure Î¼] :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» a, Ï a / Î¼ a) (v.filter_at x) (ğ“ 0) :=
begin
  have A : âˆ€ Îµ > (0 : â„â‰¥0), âˆ€áµ x âˆ‚Î¼, âˆ€á¶  a in v.filter_at x, Ï a < Îµ * Î¼ a,
  { assume Îµ Îµpos,
    set s := {x | Â¬(âˆ€á¶  a in v.filter_at x, Ï a < Îµ * Î¼ a) } with hs,
    change Î¼ s = 0,
    obtain âŸ¨o, o_meas, Ïo, Î¼oâŸ© : âˆƒ (o : set Î±), measurable_set o âˆ§ Ï o = 0 âˆ§ Î¼ oá¶œ = 0 := hÏ,
    apply le_antisymm _ bot_le,
    calc Î¼ s â‰¤ Î¼ ((s âˆ© o) âˆª oá¶œ) : begin
      conv_lhs { rw â† inter_union_compl s o },
      exact measure_mono (union_subset_union_right _ (inter_subset_right _ _))
    end
    ... â‰¤ Î¼ (s âˆ© o) + Î¼ (oá¶œ) : measure_union_le _ _
    ... = Î¼ (s âˆ© o) : by rw [Î¼o, add_zero]
    ... = Îµâ»Â¹ * (Îµ â€¢ Î¼) (s âˆ© o) : begin
      simp only [measure.coe_nnreal_smul, algebra.mul_smul_comm, pi.smul_apply],
      simp only [has_scalar.smul, has_scalar.comp.smul, ennreal.coe_of_nnreal_hom, â† mul_assoc],
      rw [ennreal.mul_inv_cancel (ennreal.coe_pos.2 Îµpos).ne' ennreal.coe_ne_top, one_mul],
    end
    ... â‰¤ Îµâ»Â¹ * Ï (s âˆ© o) : begin
      apply ennreal.mul_le_mul le_rfl,
      refine v.measure_le_of_frequently_le Ï ((measure.absolutely_continuous.refl Î¼).smul Îµ) _ _,
      assume x hx,
      rw hs at hx,
      simp only [mem_inter_eq, not_lt, not_eventually, mem_set_of_eq] at hx,
      exact hx.1
    end
    ... â‰¤ Îµâ»Â¹ * Ï o : ennreal.mul_le_mul le_rfl (measure_mono (inter_subset_right _ _))
    ... = 0 : by rw [Ïo, mul_zero] },
  obtain âŸ¨u, u_anti, u_pos, u_limâŸ© :
    âˆƒ (u : â„• â†’ â„â‰¥0), strict_anti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ tendsto u at_top (ğ“ 0) :=
      exists_seq_strict_anti_tendsto (0 : â„â‰¥0),
  have B : âˆ€áµ x âˆ‚Î¼, âˆ€ n, âˆ€á¶  a in v.filter_at x, Ï a < u n * Î¼ a :=
    ae_all_iff.2 (Î» n, A (u n) (u_pos n)),
  filter_upwards [B, v.ae_eventually_measure_pos],
  assume x hx h'x,
  refine tendsto_order.2 âŸ¨Î» z hz, (ennreal.not_lt_zero hz).elim, Î» z hz, _âŸ©,
  obtain âŸ¨w, w_pos, w_ltâŸ© : âˆƒ (w : â„â‰¥0), (0 : â„â‰¥0âˆ) < w âˆ§ (w : â„â‰¥0âˆ) < z :=
    ennreal.lt_iff_exists_nnreal_btwn.1 hz,
  obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < w :=
    ((tendsto_order.1 u_lim).2 w (ennreal.coe_pos.1 w_pos)).exists,
  filter_upwards [hx n, h'x, v.eventually_measure_lt_top x],
  assume a ha Î¼a_pos Î¼a_lt_top,
  rw ennreal.div_lt_iff (or.inl Î¼a_pos.ne') (or.inl Î¼a_lt_top.ne),
  exact ha.trans_le (ennreal.mul_le_mul ((ennreal.coe_le_coe.2 hn.le).trans w_lt.le) le_rfl)
end

/-- A set of points `s` satisfying both `Ï a â‰¤ c * Î¼ a` and `Ï a â‰¥ d * Î¼ a` at arbitrarily small
sets in a Vitali family has measure `0` if `c < d`. Indeed, the first inequality should imply
that `Ï s â‰¤ c * Î¼ s`, and the second one that `Ï s â‰¥ d * Î¼ s`, a contradiction if `0 < Î¼ s`. -/
theorem null_of_frequently_le_of_frequently_ge [sigma_compact_space Î±] [borel_space Î±]
  {Ï : measure Î±} [is_locally_finite_measure Ï] [is_locally_finite_measure Î¼]
  (hÏ : Ï â‰ª Î¼) {c d : â„â‰¥0} (hcd : c < d) (s : set Î±)
  (hc : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filter_at x, Ï a â‰¤ c * Î¼ a)
  (hd : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filter_at x, (d : â„â‰¥0âˆ) * Î¼ a â‰¤ Ï a) :
  Î¼ s = 0 :=
begin
  apply null_of_locally_null s (Î» x hx, _),
  obtain âŸ¨o, xo, o_open, Î¼oâŸ© : âˆƒ o : set Î±, x âˆˆ o âˆ§ is_open o âˆ§ Î¼ o < âˆ :=
    measure.exists_is_open_measure_lt_top Î¼ x,
  refine âŸ¨o, mem_nhds_within_of_mem_nhds (o_open.mem_nhds xo), _âŸ©,
  let s' := s âˆ© o,
  by_contra,
  apply lt_irrefl (Ï s'),
  calc Ï s' â‰¤ c * Î¼ s' : v.measure_le_of_frequently_le (c â€¢ Î¼) hÏ s' (Î» x hx, hc x hx.1)
  ... < d * Î¼ s' : begin
    apply (ennreal.mul_lt_mul_right _ _).2 (ennreal.coe_lt_coe.2 hcd),
    { assume h', exact h h' },
    { exact (lt_of_le_of_lt (measure_mono (inter_subset_right _ _)) Î¼o).ne },
  end
  ... â‰¤ Ï s' : v.measure_le_of_frequently_le Ï
    ((measure.absolutely_continuous.refl Î¼).smul d) s' (Î» x hx, hd x hx.1)
end

end vitali_family
